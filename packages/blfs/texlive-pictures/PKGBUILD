#-*-mode:sh-*-
# Contributor: alexforsale@yahoo.com
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
pkgname=texlive-pictures
pkgver=2019.50872
_revnr=${pkgver#2019.}
pkgrel=1
pkgdesc="TeX Live - Packages for drawings graphics"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('blfs' 'typesetting')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "$pkgname.maps"
        "xypdf-rev1.21.patch")
options=('!emptydirs')

build() {
    cd "$srcdir"
    for p in *.tar.xz; do
	bsdtar -xf $p
    done
    rm -rf {tlpkg,doc,source} || true

    #patch -Np1 -i "${srcdir}/xypdf-rev1.21.patch" -d "${srcdir}/tex/generic/xypic"
}

package() {
    cd "$srcdir"
    install -m755 -d $pkgdir/var/local/lib/texmf/arch/installedpkgs
    sed -i '/^#/d' CONTENTS
    install -m644 CONTENTS $pkgdir/var/local/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
    install -m644 $pkgname.maps $pkgdir/var/local/lib/texmf/arch/installedpkgs/
    install -m755 -d $pkgdir/usr/local/share
    wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
    for dir in $wanteddirs; do
        find $dir -type d -exec install -d -m755 $pkgdir/usr/local/share/texmf-dist/'{}' \;
        find $dir -type f -exec install -m644 '{}' $pkgdir/usr/local/share/texmf-dist/'{}' \;
    done
    if [[ -d texmf-dist ]]; then
        find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/local/share/'{}' \;
        find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/local/share/'{}' \;
    fi
    if [[ -d $pkgdir/usr/local/share/texmf-dist/scripts ]]; then
        find $pkgdir/usr/local/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
    fi

    _linked_scripts="
cachepic/cachepic.tlu
epspdf/epspdftk.tcl
epspdf/epspdf.tlu
fig4latex/fig4latex
getmap/getmapdl.lua
mathspic/mathspic.pl
mkpic/mkpic
petri-nets/pn2pdf
"
    install -m755 -d $pkgdir/usr/local/bin
    for _script in ${_linked_scripts}; do
        _scriptbase=$(basename $_script)
        _scriptbase=${_scriptbase%.*}
        ln -s /usr/local/share/texmf-dist/scripts/${_script} ${pkgdir}/usr/local/bin/${_scriptbase}
    done
}

sha256sums=('afed6d984ced7731dfb79d67ef03177dadc1f6a39f7f16e3664986622ec6879d'
            '847676e2322065219992ef249a68cddb26d71e192e8d59db58f264c5e11c8be9'
            '26e3307acdf7597305e0b4a4b6254a67f86581b53eea9982c767678db32164a5')
