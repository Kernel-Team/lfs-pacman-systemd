#-*-mode:sh-*-
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>
# Contributor: alexforsale@yahoo.com
pkgname=dhcpcd
pkgver=8.0.3
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('x86_64')
license=('BSD')
groups=('blfs' 'network' 'lfs-base')
depends=('glibc' 'sh' 'udev' 'libsystemd')
optdepends=('openresolv: resolvconf support')
provides=('dhcp-client')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("https://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.xz"
        'dhcpcd_.service'
        'dhcpcd.service')
validpgpkeys=('A785ED2755955D9E93EA59F6597F97EA9AD45549')  # Roy Marples (NetBSD) <roy@NetBSD.org>

prepare(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --libexecdir=/usr/lib/dhcpcd \
        --dbdir=/var/lib/dhcpcd \
        --rundir=/run
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make test
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Install License
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" \
	    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    # Set Options in /etc/dhcpcd.conf
    echo noipv4ll >> "${pkgdir}/etc/dhcpcd.conf" # Disable ip4vall

    # install systemd files
    install -Dm644 "${srcdir}/dhcpcd_.service" "${pkgdir}/lib/systemd/system/dhcpcd@.service"
    install -Dm644 "${srcdir}/dhcpcd.service" "${pkgdir}/lib/systemd/system/dhcpcd.service" # FS#31543
}

sha256sums=('9674971fcd5acd5a3401a0ad8eba7f0b816fec9abb4a2520332b4d8dae068f1d'
            '4829112d0d04cde6955b56aa3876f6ce0780e9dd2ade70831f12d3c4ce767081'
            '3e1900bead9ddcc7ec734f6d5c7e797f82dad60854c2cdc174dfc169dc5170dd')
