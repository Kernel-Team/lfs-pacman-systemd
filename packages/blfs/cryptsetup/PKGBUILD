#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=cryptsetup
pkgver=2.2.1
pkgrel=1
pkgdesc='Userspace setup tool for transparent encryption of block devices using dm-crypt'
arch=(x86_64)
license=('GPL')
url='https://gitlab.com/cryptsetup/cryptsetup/'
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'libutil-linux' 'json-c' 'argon2')
makedepends=('util-linux')
options=('!emptydirs')
validpgpkeys=('2A2918243FDE46648D0686F9D9B0577BD93E98FC') # Milan Broz <gmazyland@gmail.com>
source=("https://www.kernel.org/pub/linux/utils/cryptsetup/v${pkgver%.*}/${pkgname}-${pkgver}.tar."{xz,sign}
        'hooks-encrypt'
        'install-encrypt'
        'install-sd-encrypt')
groups=('blfs' 'security')

build() {
    cd "${srcdir}"/$pkgname-${pkgver}

    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --enable-libargon2 \
        --disable-static
    make
}

package() {
    cd "${srcdir}"/$pkgname-${pkgver}

    make DESTDIR="${pkgdir}" install

    # install hook
    install -D -m0644 "${srcdir}"/hooks-encrypt "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
    install -D -m0644 "${srcdir}"/install-encrypt "${pkgdir}"/usr/lib/initcpio/install/encrypt
    install -D -m0644 "${srcdir}"/install-sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt
}

sha256sums=('94e79a31ed38bdb0acd9af7ccca1605a2ac62ca850ed640202876b1ee11c1c61'
            'SKIP'
            '416aa179ce3c6a7a5eee0861f1f0a4fafac91b69e84a2aae82b6e5a6140e31e2'
            '7a38b3cc0e12f0a212456f1eb08e673548e049e0c50a15e245fc5d8033413786'
            '84ca0767412bb02a1f29d2164bea36e471531a24b4c864f2a680c0eb3cfe2498')
