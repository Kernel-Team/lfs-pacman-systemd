#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=doxygen
pkgname=('doxygen')
pkgver=1.8.16
pkgrel=1
pkgdesc='Documentation system for C++, C, Java, IDL and PHP'
url='http://www.doxygen.org/'
arch=('x86_64')
license=('GPL')
_makedeps=('qt5-base' 'texlive-core' 'ghostscript' 'texlive-latexextra' 'graphviz')
makedepends=('cmake' 'gcc-libs' 'flex' 'python2')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/doxygen/doxygen/archive/Release_${pkgver//./_}.tar.gz)
groups=('blfs' 'programming')

for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        depends+=( "${_m}")
        pkgname+=( 'doxygen-docs')
    fi
done

prepare() {
    cd ${pkgbase}-Release_${pkgver//./_}

    mkdir build

    # Install the man pages in the right place
    sed -i 's:DESTINATION man/man1:DESTINATION "${CMAKE_INSTALL_PREFIX}/share/man/man1":g' \
        doc/CMakeLists.txt

    cd build
    _confargs=("-DCMAKE_INSTALL_PREFIX:PATH=/usr" "-DPYTHON_EXECUTABLE:FILE=/usr/bin/python2")
    if [[ -n "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        _confargs+=( "-Dbuild_doc:BOOL=ON" "-Dbuild_wizard:BOOL=ON" "-DDOC_INSTALL_DIR:PATH=share/doc/doxygen")
    fi

    cmake .. ${_confargs[@]}
}

build() {
    cd ${pkgbase}-Release_${pkgver//./_}/build

    make
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        make docs
    fi
}

check(){
    cd ${pkgbase}-Release_${pkgver//./_}/build
    make tests
}

package_doxygen() {
    pkgdesc='Documentation system for C++, C, Java, IDL and PHP'
    depends=('gcc-libs')
    optdepends=('graphviz: for caller/callee graph generation'
                'qt5-base: for doxywizard')

    cd ${pkgbase}-Release_${pkgver//./_}/build
    make DESTDIR="${pkgdir}" install

    # Docs are in 'doxygen-docs'
    rm -rf "${pkgdir}/usr/share/doc"
}

package_doxygen-docs() {
    pkgdesc='Developer documentation for doxygen'

    cd ${pkgbase}-Release_${pkgver//./_}/build
    make DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/share/man"
}

md5sums=('cedf668626137a6b9251a7996ccc38d3')
