#-*-mode:sh-*-
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
pkgname=mdadm
pkgver=4.1
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=(x86_64)
license=('GPL')
url="https://git.kernel.org/cgit/utils/mdadm"
groups=('base')
depends=('glibc')
backup=('etc/mdadm.conf')
source=("https://www.kernel.org/pub/linux/utils/raid/mdadm/mdadm-4.1.tar."{xz,sign}
        mdadm.conf 
        "$pkgname.config")
replaces=('raidtools')
validpgpkeys=('6A86B80E1D22F21D0B26BA75397D82E0531A9C91') # Jes Sorensen

prepare(){
    cd $srcdir/$pkgname-$pkgver
    sed 's@-Werror@@' -i Makefile
}

build() {
    cd $srcdir/$pkgname-$pkgver
    make CXFLAGS="$CFLAGS"
}

check(){
    cd $srcdir/$pkgname-$pkgver
    sed -i 's# if.* == "1"#& -a -e $targetdir/log#' test
    make test
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make INSTALL=/usr/bin/install DESTDIR=$pkgdir install
    make SYSTEMD_DIR=$pkgdir/lib/systemd/system install-systemd
    install -D -m644 ../mdadm.conf $pkgdir/etc/mdadm.conf

    install -vdm755 $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
    install -m644 ../$pkgname.config $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
}

md5sums=('51bf3651bd73a06c413a2f964f299598'
         'SKIP'
         '5a37c112aa07dccdde62f9fa5b888607'
         '9027aa3806c27f090c0fc9c6556640a2')
