#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=xkeyboard-config
pkgver=2.27.r4.g3ccf219
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="https://www.freedesktop.org/wiki/Software/XKeyboardConfig"
makedepends=('intltool' 'xorg-xkbcomp' 'libxslt')
provides=('xkbdata')
replaces=('xkbdata')
conflicts=('xkbdata')
source=(xorg/xkeyboard-config::git://anongit.freedesktop.org/git/xkeyboard-config)
validpgpkeys=('FFB4CCD275AAA422F5F9808E0661D98FC933A145')
validpgpkeys+=('15CFA5C595041D2CCBEA155F1732AA424A0E86B4') # "Sergey Udaltsov (For GNOME-related tasks) <svu@gnome.org>"
sha512sums=('SKIP')
groups=('blfs' 'xorg')

pkgver() {
    cd "$pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/${pkgname//-/.}.//g"
}

prepare(){
    cd "$pkgname"
    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr \
                --with-xkb-base=/usr/share/X11/xkb \
                --with-xkb-rules-symlink=xorg \
                --enable-compat-rules=yes
}

build() {
    cd "$pkgname"
    make
}

package() { 
    cd "$pkgname"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/X11/xkb/compiled"

    install -m755 -d "${pkgdir}/var/lib/xkb"
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
