#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
pkgname=libvorbis
pkgver=1.3.6
pkgrel=1
pkgdesc='Vorbis codec library'
arch=('x86_64')
url='https://www.xiph.org/vorbis/'
license=('BSD')
depends=('libogg')
provides=('libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so')
source=("https://downloads.xiph.org/releases/vorbis/libvorbis-${pkgver}.tar.gz")
sha256sums=('6ed40e0241089a42c48604dc00e362beee00036af2d8b3f46338031c9e0351cb')
groups=('blfs' 'multimedia-libraries-drivers')

prepare(){
    cd libvorbis-${pkgver}
    ./configure \
        --prefix='/usr' \
        --disable-static \
        --with-ogg=/usr
}

build() {
    cd libvorbis-${pkgver}
    make
}

check() {
    cd libvorbis-${pkgver}

    make -j1 check
}

package() {
    cd libvorbis-${pkgver}

    make DESTDIR="${pkgdir}" install
    install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libvorbis
}
