#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=libcroco
pkgver=0.6.13
pkgrel=1
pkgdesc="A CSS parsing library"
url="https://gitlab.gnome.org/GNOME/libcroco"
arch=(x86_64)
depends=(glib2 libxml2)
_makedeps=(gtk-doc)
makedepends=(intltool git)
license=(LGPL)
_commit=7e15ca6c2c29a4b78367e6efa6195b331a92b2a7  # tags/0.6.13^0
source=("git+https://gitlab.gnome.org/GNOME/libcroco.git#commit=$_commit")
sha256sums=('SKIP')
groups=('blfs' 'general-libraries')

_confargs=('--prefix=/usr' "--disable-static")
for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
        _confargs+=( '--enable-gtk-doc')
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}")
    fi
done

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $pkgname
    NOCONFIGURE=1 ./autogen.sh
    ./configure ${_confargs[@]}
}

check() {
    cd $pkgname
    make check
}

build() {
    cd $pkgname
    make
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}
