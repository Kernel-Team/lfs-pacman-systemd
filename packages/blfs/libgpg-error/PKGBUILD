#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=libgpg-error
pkgver=1.36
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('glibc' 'sh')
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig}
        gawk5.diff)
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06') # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
groups=('blfs' 'general-libraries')

prepare(){
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../gawk5.diff
    autoreconf -vfi
    ./configure --prefix=/usr
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check || true
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}/" install
}

sha1sums=('5245ce38b198cecd57775fb3c7b7328ec1a2a615'
          'SKIP'
          '0f4e2308e0ad623c7066f9437fd0219ee1544d2b')
