#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>
pkgname=libogg
pkgver=1.3.4
pkgrel=1
pkgdesc='Ogg bitstream and framing library'
arch=(x86_64)
url=https://www.xiph.org/ogg/
license=(BSD)
depends=(glibc)
makedepends=(
    cmake
    git
    ninja
)
source=(git+https://github.com/xiph/ogg.git?signed#tag=3328abd152508614f7ce4cd491dc98d14eba7ffc)
sha256sums=(SKIP)
validpgpkeys=(B7B00AEE1F960EEA0FED66FB9259A8F2D2D44C84) # Ralph Giles <giles@thaumas.net>
groups=('blfs' 'multimedia-libraries-drivers')

pkgver() {
    cd ogg

    git describe --tags | sed 's/^v//'
}

prepare() {
    if [[ -d build ]]; then
        rm -rf build
    fi
    mkdir build
    cd build
    cmake ../ogg \
          -G Ninja \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DBUILD_SHARED_LIBS=ON
}

build() {
    cd build
    ninja
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm 644 ogg/COPYING -t "${pkgdir}"/usr/share/licenses/libogg/
}
