#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=glib2
pkgname=(glib2)
pkgver=2.62.0
pkgrel=1
pkgdesc="Low level core library"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
arch=(x86_64)
depends=(pcre libffi libutil-linux zlib)
_makedeps=(gtk-doc shared-mime-info)
makedepends=(gettext python libelf git util-linux meson dbus)
checkdepends=(desktop-file-utils)
optdepends=('python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
            'libelf: gresource inspection tool')
options=(!emptydirs)
_commit=a1af0be78c6cb3bce4791a1598a796c3e912019f  # tags/2.62.0^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        noisy-glib-compile-schemas.diff
        glib-compile-schemas.hook gio-querymodules.hook)
sha256sums=('SKIP'
            '81a4df0b638730cffb7fa263c04841f7ca6b9c9578ee5045db6f30ff0c3fc531'
            'e1123a5d85d2445faac33f6dae1085fdd620d83279a4e130a83fe38db52b62b3'
            '5ba204a2686304b1454d401a39a9d27d09dd25e4529664e3fd565be3d439f8b6')
groups=('blfs' 'general-libraries')

for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}")
    fi
done

_confargs=('--prefix=/usr' '-D selinux=disabled')
if [[ -n "$(pacman -Qq "${_makedeps[@]}" 2>/dev/null)" ]];then
    _confargs+=( '-D man=true' '-D gtk_doc=true')
    pkgname+=( 'glib2-docs')
fi

pkgver() {
    cd glib
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd glib

    # Suppress noise from glib-compile-schemas.hook
    patch -Np1 -i ../noisy-glib-compile-schemas.diff

    cd ..
    meson glib build ${_confargs[@]}
}

build() {
    
    ninja -C build
}

check() {
    meson test -C build
}

package_glib2() {
    DESTDIR="$pkgdir" meson install -C build
    install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook

    python -m compileall -d /usr/share/glib-2.0/codegen "$pkgdir/usr/share/glib-2.0/codegen"
    python -O -m compileall -d /usr/share/glib-2.0/codegen "$pkgdir/usr/share/glib-2.0/codegen"

    # Split docs
    if [[ "$(pacman -Qq "${_makedeps[@]}" 2>/dev/null)" ]];then
        mv "$pkgdir/usr/share/gtk-doc" "$srcdir"
    fi
}

package_glib2-docs() {
    pkgdesc="Documentation for GLib"
    depends=()
    optdepends=()
    license+=(custom)

    mkdir -p "$pkgdir/usr/share"
    mv gtk-doc "$pkgdir/usr/share"

    install -Dt "$pkgdir/usr/share/licenses/glib2-docs" -m644 glib/docs/reference/COPYING
}
