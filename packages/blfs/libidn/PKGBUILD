#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=libidn
pkgver=1.35
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
url="https://www.gnu.org/software/libidn/"
arch=('x86_64')
license=('GPL3' 'LGPL')
depends=('glibc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C'
              '1CB27DBC98614B2D5841646D08302DB6A2670428')
groups=('blfs' 'general-libraries')

prepare(){
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --disable-static
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    mkdir -p ${pkgdir}/usr/share/gtk-doc/html/${pkgname}
    cp -r -v doc/reference/html/* ${pkgdir}/usr/share/gtk-doc/html/${pkgname}
}

sha256sums=('f11af1005b46b7b15d057d7f107315a1ad46935c7fcdf243c16e46ec14f0fe1e'
            'SKIP')
