#-*-mode:sh-*-
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=wpa_supplicant
pkgver=2.9
pkgrel=1
pkgdesc='A utility providing key negotiation for WPA wireless networks'
url='http://hostap.epitest.fi/wpa_supplicant'
arch=(x86_64)
license=(GPL)
depends=(openssl libdbus readline libnl)
install=wpa_supplicant.install
source=(https://w1.fi/releases/${pkgname}-${pkgver}.tar.gz{,.asc}
        config ${pkgname}.config)
validpgpkeys=('EC4AA0A991A5F2464582D52D2B6EF432EFC895FA') # Jouni Malinen
groups=('blfs' 'networking-programs')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    local i; for i in "${source[@]}"; do
                 case $i in
                     *.patch)
                         msg2 "Applying patch $i"
                         patch -p1 -i "$srcdir/$i"
                         ;;
                 esac
             done

    cd "$srcdir/$pkgname-$pkgver/$pkgname"
    cp "$srcdir/config" ./.config
}

build() {
    cd "$srcdir/$pkgname-$pkgver/$pkgname"

    make LIBDIR=/usr/lib BINDIR=/sbin
    make LIBDIR=/usr/lib BINDIR=/sbin eapol_test
}

package() {
    cd "$srcdir/$pkgname-$pkgver/$pkgname"
    make LIBDIR=/usr/lib BINDIR=/sbin DESTDIR="$pkgdir" install
    install -Dm755 eapol_test "$pkgdir/usr/bin/eapol_test"

    install -d -m755 "$pkgdir/etc/wpa_supplicant"
    install -Dm644 wpa_supplicant.conf \
            "$pkgdir/usr/share/doc/wpa_supplicant/wpa_supplicant.conf"


    install -d -m755 "$pkgdir/usr/share/dbus-1/system-services"
    install -m644 \
            dbus/fi.w1.wpa_supplicant1.service \
            "$pkgdir/usr/share/dbus-1/system-services/"

    install -Dm644 dbus/dbus-wpa_supplicant.conf \
            "$pkgdir/etc/dbus-1/system.d/wpa_supplicant.conf"

    install -d -m755 "$pkgdir/usr/share/man/man"{5,8}
    install -m644 doc/docbook/*.5 "$pkgdir/usr/share/man/man5/"
    install -m644 doc/docbook/*.8 "$pkgdir/usr/share/man/man8/"
    rm -f "$pkgdir/usr/share/man/man8/wpa_"{priv,gui}.8

    install -d -m755 "$pkgdir/lib/systemd/system"
    install -m644 systemd/*.service "$pkgdir/lib/systemd/system/"

    install -vdm755 $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
    install -m644 ${srcdir}/$pkgname.config $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
}

sha256sums=('fcbdee7b4a64bea8177973299c8c824419c413ec2e3a95db63dd6a5dc3541f17'
            'SKIP'
            '23aee0597750ec21b37654b5163e2f577c1204fc33bdfbf7bc2fb470e8a467db'
            '1d9a245727f31c07b02ca6bc9765c761f8e710f280221f7646643e2f757e22b2')
