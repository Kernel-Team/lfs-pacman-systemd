#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=mercurial
pkgver=5.1.1
pkgrel=1
pkgdesc='A scalable distributed SCM tool'
arch=(x86_64)
url="https://www.mercurial-scm.org/"
license=(GPL)
depends=(python2)
optdepends=('tk: for the hgk GUI')
backup=(etc/mercurial/hgrc)
validpgpkeys=(2BCCE14F5C6725AA2EA8AEB7B9C9DC824AA5BDD5
              3A8155163D0E20A530FCB78647A67FFAA346AACE)  
source=(https://www.mercurial-scm.org/release/${pkgname}-${pkgver}.tar.gz{,.asc}
        mercurial.profile)
groups=('blfs' 'programming')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e 's#env python#env python2#' mercurial/lsprof.py
}

build() {
    cd $pkgname-$pkgver/contrib/chg
    make
}

package() {
    cd $pkgname-$pkgver
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir/usr/share/man/"{man1,man5}
    install -m644 doc/hg.1 "$pkgdir/usr/share/man/man1"
    install -m644 doc/{hgrc.5,hgignore.5} "$pkgdir/usr/share/man/man5"
    install -m755 contrib/hgk "$pkgdir/usr/bin"
    install -m644 -D contrib/zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hg"
    install -m644 -D contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/hg"
    install -d "$pkgdir/usr/share/emacs/site-lisp"
    install -m644 contrib/{mq.el,mercurial.el} "$pkgdir/usr/share/emacs/site-lisp"

    install -Dm644 contrib/vim/HGAnnotate.vim \
            "$pkgdir/usr/share/vim/vimfiles/syntax/HGAnnotate.vim"

    # set some variables
    install -m755 -d "$pkgdir/etc/profile.d"
    install -m755 "$srcdir/mercurial.profile" "$pkgdir/etc/profile.d/mercurial.sh"

    # FS#38825 - Add certs config to package
    install -m755 -d "$pkgdir/etc/mercurial"
    cat <<-EOF > "$pkgdir/etc/mercurial/hgrc"
	[web]
	cacerts = /etc/pki/tls/certs/ca-bundle.crt
	EOF

    cd contrib/chg
    make DESTDIR="$pkgdir" PREFIX=/usr install
}

sha256sums=('35fc8ba5e0379c1b3affa2757e83fb0509e8ac314cbd9f1fd133cf265d16e49f'
            'SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5')
