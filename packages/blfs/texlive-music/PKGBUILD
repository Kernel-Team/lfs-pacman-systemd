#-*-mode:sh-*-
# Contributor: alexforsale@yahoo.com
# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>
pkgname=texlive-music
pkgver=2019.50602
_revnr=${pkgver#2019.}
pkgrel=1
pkgdesc="TeX Live - Music typesetting packages"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=('python2: for scripts from the lilyglyphs packages')
groups=('blfs' 'typesetting')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
sha256sums=('ba558b73f44f956f951c32fbb76cc1666337df787a6060be2ac9a7f9b0281fe3'
            '0b3cc045390e756eb9243c0acc99a48a9c02ddab4764f4a00f8c3170660f017f')

build() {
    for p in *.tar.xz; do
	bsdtar -xf $p
    done
    rm -rf {tlpkg,doc,source} || true
}

package() {
    install -m755 -d $pkgdir/var/local/lib/texmf/arch/installedpkgs
    sed -i '/^#/d' CONTENTS
    install -m644 CONTENTS $pkgdir/var/local/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
    install -m644 $pkgname.maps $pkgdir/var/local/lib/texmf/arch/installedpkgs/
    install -m755 -d $pkgdir/usr/local/share
    wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
    for dir in $wanteddirs; do
        find $dir -type d -exec install -d -m755 $pkgdir/usr/local/share/texmf-dist/'{}' \;
        find $dir -type f -exec install -m644 '{}' $pkgdir/usr/local/share/texmf-dist/'{}' \;
    done
    if [[ -d texmf-dist ]]; then
        find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/local/share/'{}' \;
        find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/local/share/'{}' \;
    fi
    if [[ -d $pkgdir/usr/local/share/texmf-dist/scripts ]]; then
        find $pkgdir/usr/local/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
    fi
    # fix python2 command name in scripts.
    sed -i 's/env python/env python2/' $pkgdir/usr/local/share/texmf-dist/scripts/lilyglyphs/*.py
    # script symlinks
    install -m755 -d $pkgdir/usr/local/bin
    ln -s /usr/local/share/texmf-dist/scripts/lilyglyphs/lily-glyph-commands.py $pkgdir/usr/local/bin/lily-glyph-commands
    ln -s /usr/local/share/texmf-dist/scripts/lilyglyphs/lily-image-commands.py $pkgdir/usr/local/bin/lily-image-commands
    ln -s /usr/local/share/texmf-dist/scripts/lilyglyphs/lily-rebuild-pdfs.py $pkgdir/usr/local/bin/lily-rebuild-pdfs
    ln -s /usr/local/share/texmf-dist/scripts/m-tx/m-tx.lua $pkgdir/usr/local/bin/m-tx
    ln -s /usr/local/share/texmf-dist/scripts/musixtex/musixflx.lua $pkgdir/usr/local/bin/musixflx
    ln -s /usr/local/share/texmf-dist/scripts/musixtex/musixtex.lua $pkgdir/usr/local/bin/musixtex
    ln -s /usr/local/share/texmf-dist/scripts/pmxchords/pmxchords.lua $pkgdir/usr/local/bin/pmxchords
}
