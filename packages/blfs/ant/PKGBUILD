#-*-mode:sh-*-
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgbase=ant
pkgname=('ant' 'ant-doc')
pkgver=1.10.7
pkgrel=1
pkgdesc='Java based build tool'
url='https://ant.apache.org/'
arch=('any')
license=('APACHE')
makedepends=('bash' 'java-environment' 'junit' 'java-hamcrest')
source=(https://www.apache.org/dist/ant/source/apache-${pkgbase}-${pkgver}-src.tar.bz2{,.asc}
        ant.conf)
validpgpkeys=(
    'CE8075A251547BEE249BC151A2115AE15F6B8B72' # Stefan Bodewig
    '8DA70C00DF7AF1B0D2F9DC74DDBCC1270A29D081' # jaikiran@apache <jaikiran@apache.org>
)
groups=('blfs' 'programming' 'java-extra')

_replace_lib() {
    # explicitly call rm to ensure we replace instead of add
    rm "$2"
    ln -s "$1" "$2"
}

prepare() {
    cd apache-${pkgbase}-${pkgver}
    sed -i 's/--add-modules java.activation/-html4/' build.xml
    sed -i 's|/usr/bin/python|/usr/bin/python2|' src/script/runant.py
}

build() {
    cd apache-${pkgbase}-${pkgver}
    #export JAVA_HOME=/usr/lib/jvm/default
    ./bootstrap.sh
    bootstrap/bin/ant -Ddest=optional -f fetch.xml
    _replace_lib /usr/share/java/junit.jar lib/optional/junit-4.12.jar
    _replace_lib /usr/share/java/hamcrest-core.jar lib/optional/hamcrest-core-1.3.jar
    _replace_lib /usr/share/java/hamcrest-library.jar lib/optional/hamcrest-library-1.3.jar
    bootstrap/bin/ant dist
}

package_ant() {
    depends=('java-environment' 'bash')
    optdepends=('junit: junit tasks'
                'java-hamcrest: junit tasks')
    replaces=('apache-ant')
    provides=('apache-ant')
    conflicts=('apache-ant')
    backup=('etc/ant/ant.conf')

    local _ant_home=/opt/ant

    cd apache-${pkgbase}-${pkgver}
    ./build.sh -Ddist.dir=${pkgdir}/opt/ant dist

    cd ../apache-${pkgbase}-${pkgver}/apache-${pkgbase}-${pkgver}

    install -d "${pkgdir}${_ant_home}"
    cp -Rp etc "${pkgdir}${_ant_home}"

    find bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
         -exec install -Dm 755 {} -t "${pkgdir}${_ant_home}/bin" \;
    #install -d "${pkgdir}/usr/bin"
    #ln -s /usr/share/ant/bin/ant "${pkgdir}/usr/bin/ant"

    install -Dm 644 "${srcdir}/ant.conf" -t "${pkgdir}/etc/ant"
    install -Dm 644 lib/*.jar -t "${pkgdir}/opt/ant/lib"
    #ln -s /usr/share/java/ant "${pkgdir}${_ant_home}/lib"

    install -Dm 644 ../{LICENSE,NOTICE} -t "${pkgdir}/usr/share/licenses/${pkgname}"

    install -vdm755 ${pkgdir}/etc/profile.d/
    cat > ${pkgdir}/etc/profile.d/ant.sh << EOF
# Begin /etc/profile.d/ant.sh

pathappend /opt/ant/bin
export ANT_HOME=/opt/ant

# End /etc/profile.d/ant.sh
EOF
}

package_ant-doc() {
    pkgdesc='Apache Ant build tool documentation'
    replaces=('apache-ant-doc')
    provices=('apache-ant-doc')
    conflicts=('apache-ant-doc')

    install -d "${pkgdir}/opt/doc/ant"
    cp -r apache-${pkgbase}-${pkgver}/apache-${pkgbase}-${pkgver}/manual/* \
       "${pkgdir}/opt/doc/ant"
}

sha256sums=('39882eff327f4a056d77605f0a973d4f30d003083e54fa0885ddab6055b6c8de'
            'SKIP'
            '6c9954ced9812ed727b374617f5e5a34646a3a93acac94f56d6e0ffe9feb9157')
