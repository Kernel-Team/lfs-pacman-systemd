#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bï¿½chler <thomas.archlinux.org>
pkgname=ntfs-3g
_pkgname=ntfs-3g_ntfsprogs
pkgver=2017.3.23
pkgrel=3
pkgdesc='NTFS filesystem driver and utilities'
url='https://www.tuxera.com/community/open-source-ntfs-3g/'
arch=('x86_64')
license=('GPL2')
depends=('util-linux' 'fuse2')
source=("https://tuxera.com/opensource/${_pkgname}-${pkgver}.tgz"
        'mftmirr.patch')
sha256sums=('3e5a021d7b761261836dcb305370af299793eedbded731df3d6943802e1262d5'
            '4166ff3021921d7a7ffac8f5de64b5735b1d5e365a024744bebc7260cf1ce726')

conflicts=('ntfsprogs')
provides=('ntfsprogs')
replaces=('ntfsprogs')
groups=('blfs' 'file-systems-disk-management')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    sed 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' -i {ntfsprogs,src}/Makefile.in
    patch -p1 -i ../mftmirr.patch
    ./configure \
	--prefix=/usr \
	--mandir=/usr/share/man \
	--disable-ldconfig \
	--disable-static \
	--with-fuse=external \
	--enable-xattr-mappings \
	--enable-posix-acls \
	--enable-extras
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make
}

package_ntfs-3g() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -vdm755 ${pkgdir}/lib
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}"/usr/share/man/man8/ntfsfallocate.8 # uninstalled binary
    install -vdm755 ${pkgdir}/sbin
    ln -sv ../bin/ntfs-3g ${pkgdir}/sbin/mount.ntfs
}
