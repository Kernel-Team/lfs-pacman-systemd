#-*-mode:sh-*-
# Contributor: alexforsale@yahoo.com
# Maintainer: RÃ©my Oudompheng <remy at archlinux.org>
pkgname=texlive-humanities
pkgver=2019.50921
_revnr=${pkgver#2019.}
pkgrel=1
pkgdesc="TeX Live -  LaTeX packages for law, linguistics, social sciences, and humanities"
license=('GPL')
arch=(any)
depends=('texlive-core' 'texlive-latexextra')
optdepends=('texlive-pictures: for package qtree')
groups=('blfs' 'typesetting')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
options=('!emptydirs')
sha256sums=('378d35c7fc5132e38c57e3b27619af53f57dec4dcb21fec621311b6b578ce4a7')

build() {
    cd "$srcdir"
    for p in *.tar.xz; do
	bsdtar -xf $p
    done
    rm -rf {tlpkg,doc,source} || true
}

package() {
    install -m755 -d $pkgdir/var/local/lib/texmf/arch/installedpkgs
    sed -i '/^#/d' CONTENTS
    install -m644 CONTENTS $pkgdir/var/local/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
    install -m755 -d $pkgdir/usr/local/share
    wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
    for dir in $wanteddirs; do
        find $dir -type d -exec install -d -m755 $pkgdir/usr/local/share/texmf-dist/'{}' \;
        find $dir -type f -exec install -m644 '{}' $pkgdir/usr/local/share/texmf-dist/'{}' \;
    done
    if [[ -d texmf-dist ]]; then
        find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/local/share/'{}' \;
        find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/local/share/'{}' \;
    fi
    if [[ -d $pkgdir/usr/local/share/texmf-dist/scripts ]]; then
        find $pkgdir/usr/local/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
    fi

    install -m755 -d $pkgdir/usr/local/bin
    ln -s /usr/local/share/texmf-dist/scripts/diadia/diadia.lua ${pkgdir}/usr/local/bin/diadia
}
