#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=librsvg
pkgver=2.46.0
pkgrel=1
pkgdesc="SVG rendering library"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64)
license=(LGPL)
depends=(gdk-pixbuf2 pango libcroco)
makedepends=(intltool gobject-introspection vala python2 gtk-doc git rust)
_commit=b4aa8d277d30f4042797dc8b64c073b10f8bac4b  # tags/2.46.0^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit")
sha256sums=('SKIP')
groups=('blfs' 'graphics-font-libraries')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $pkgname
    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr --disable-static --enable-vala --enable-gtk-doc
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
}

build() {
    cd $pkgname
    make
}

check() {
    cd $pkgname
    make check
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}
