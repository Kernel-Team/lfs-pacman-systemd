#-*-mode:sh-*-
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgbase=gobject-introspection
pkgname=(gobject-introspection gobject-introspection-runtime)
pkgver=1.62.0
_glibver=2.62.0
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
url="https://wiki.gnome.org/Projects/GObjectIntrospection"
arch=(x86_64)
license=(LGPL GPL)
depends=(python python-mako python-markdown)
_makedeps=('cairo' 'gtk-doc')
makedepends=(git python-sphinx meson bison libffi "glib2=$_glibver")
options=(!emptydirs)
_commit=f330f517d8cd0bfbe67c62ba00be85dee2b338ed  # tags/1.62.0^0
source=("git+https://gitlab.gnome.org/GNOME/gobject-introspection.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/glib.git?signed#tag=$_glibver")
sha512sums=('SKIP'
            'SKIP')
validpgpkeys=('923B7025EE03C1C59F42684CF0942E894B2EAFA0') # Philip Withnall <philip@tecnocode.co.uk>
groups=('blfs' 'general-libraries')

# FFI_PC_CFLAGS
# FFI_PC_LIBS
# FFI_PC_PACKAGES

_confargs=( "-D doctool=true" "-D glib_src_dir=../../glib")
for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Ss "^${_m}$" 2>/dev/null)" ]];then
        depends+=( "${_m}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}")
    fi
done

if [[ "$(pacman -Qq cairo 2>/dev/null)" ]];then
    _confargs+=( '-D cairo=true')
fi
if [[ "$(pacman -Qq gtk-doc 2>/dev/null)" ]];then
    _confargs+=( '-D gtk-doc=true')
fi

pkgver() {
    cd $pkgbase
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    meson-helper $pkgbase build ${_confargs[@]}
}

build() {
    ninja -C build
}

check() {
    meson test -C build
}

package_gobject-introspection() {
    depends+=("gobject-introspection-runtime=$pkgver-$pkgrel")

    DESTDIR="$pkgdir" meson install -C build

    python -m compileall -d /usr/lib/gobject-introspection "$pkgdir/usr/lib/gobject-introspection"
    python -O -m compileall -d /usr/lib/gobject-introspection "$pkgdir/usr/lib/gobject-introspection"

    ### Split runtime
    mkdir -p "$srcdir/runtime/lib"
    mv "$pkgdir"/usr/lib/{lib*,girepository-*} "$srcdir/runtime/lib"
}

package_gobject-introspection-runtime() {
    pkgdesc+=" (runtime library)"
    depends=(glib2)

    mv "$srcdir/runtime" "$pkgdir/usr"
}
