#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
_pkgname=util-keysyms
pkgname=xcb-util-keysyms
pkgver=0.4.0.r1.g6915154
pkgrel=1
pkgdesc="Utility libraries for XC Binding - Standard X key constants and conversion to/from keycodes"
arch=('x86_64')
url="https://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros')
source=("xorg/xcb/util-keysyms::git://anongit.freedesktop.org/git/xcb/util-keysyms/"
        'LICENSE')
sha512sums=('SKIP'
            '62f83468f9d9503a5a8c1ebaac78cbaefe3c485f8bfd7192817ec59254872fe2914ed66ae8b9e7f7a6ef5af283048c11390f44eb8f0f70b1c136ef5c75282e8c')
groups=('blfs' 'xorg')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/$_pkgname.//g"
}

prepare(){
    cd "$_pkgname"
    git submodule update --init
    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr --disable-static
}

build() {
    cd "$_pkgname"
    make
}

check() {
    cd "$_pkgname"
    make check
}

package() { 
    cd "$_pkgname"
    make DESTDIR="${pkgdir}" install

    install -D -m644 "${srcdir}/LICENSE" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
