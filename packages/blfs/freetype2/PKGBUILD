#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=freetype2
pkgname=('freetype2' 'freetype2-demos' 'freetype2-docs')
pkgver=2.10.1
pkgrel=1
pkgdesc="Font rasterization library"
arch=(x86_64)
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
_deps=('harfbuzz')
depends=('zlib' 'bzip2' 'sh' 'libpng')
makedepends=('libx11')
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.xz{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.xz{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.xz{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-infinality-subpixel-hinting.patch
        0003-Enable-long-PCF-family-names.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh)
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')
groups=('blfs' 'graphics-font-libraries')

for _d in "${_deps[@]}";do
    if [[ "$(pacman -Qq "${_d}" 2>/dev/null)" ]];then
        depends+=( "${_d}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_d}")
    fi
done

prepare() {
    # Rename source dir to allow building the demos
    mv freetype-${pkgver} freetype2
    mv ft2demos-${pkgver} freetype2-demos

    cd freetype2
    patch -Np1 -i ../0001-Enable-table-validation-modules.patch
    patch -Np1 -i ../0002-Enable-infinality-subpixel-hinting.patch
    patch -Np1 -i ../0003-Enable-long-PCF-family-names.patch

    cd ../freetype2-demos
    # enable more demos
    patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch

    # Suppress RPATH
    sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
    cd freetype2
    ./configure --prefix=/usr --disable-static
    make

    # Build demos
    cd ../freetype2-demos
    make
}

check() {
    cd freetype2
    make -k check
}

package_freetype2() {
    provides=('libfreetype.so')
    install=freetype2.install
    backup=('etc/profile.d/freetype2.sh')

    cd freetype2
    make DESTDIR="${pkgdir}" install
    install -Dt "${pkgdir}/etc/profile.d" -m644 ../freetype2.sh
}

package_freetype2-demos() {
    pkgdesc="Freetype tools and demos"
    depends=('freetype2' 'libx11')

    cd freetype2-demos
    install -d "${pkgdir}/usr/bin"
    for _i in bin/{f,t}t*; do
        libtool --mode=install install $_i "${pkgdir}/usr/bin"
    done
}

package_freetype2-docs() {
    pkgdesc="Freetype documentation"
    depends=('freetype2')

    cd freetype2
    install -d "${pkgdir}/usr/share/doc"
    cp -a docs "${pkgdir}/usr/share/doc/freetype2"
}

sha1sums=('79874ef4eaa52025126b71d836453b8279bdd331'
          'SKIP'
          '5a90bba4cd376129ce371bcb4da8acc5ca0dce05'
          'SKIP'
          'bf5e24878c66404b765eab638783626d777a25ac'
          'SKIP'
          '2673720b1f4a4cd9e0267ca1c9e6150600286600'
          '3b5ad02373596c43484b53ae916ae83dee4e6896'
          '435e7b592ac2bb58ffe5ac78aedd8716a4f33b21'
          '72cfecbe738085eec475e012617661ad0cc9b76f'
          'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f')
