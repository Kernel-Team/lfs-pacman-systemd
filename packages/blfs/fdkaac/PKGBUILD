#-*-mode:sh-*-
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: alexforsale@yahoo.com
pkgname=fdkaac
pkgver=1.0.0
pkgrel=1
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=(x86_64)
url='https://github.com/nu774/fdkaac'
license=(custom)
depends=(libfdk-aac.so)
makedepends=(git)
source=(git+https://github.com/nu774/fdkaac.git#tag=${pkgver})
sha256sums=(SKIP)
groups=('blfs' 'multimedia-libraries-drivers')

prepare() {
    cd fdkaac
    autoreconf -fiv
    ./configure \
        --prefix='/usr' \
        --disable-rpath
}

build() {
    cd fdkaac
    make
}

package() {
    cd fdkaac

    make DESTDIR="${pkgdir}" install
    install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/fdkaac/
}

# vim: ts=2 sw=2 et:
