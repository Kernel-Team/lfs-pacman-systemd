#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=('rust' 'rust-docs')
pkgver=1.37.0
pkgrel=1
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')
_makedepens=('rust')
makedepends=('llvm' 'libffi' 'perl' 'python2' 'curl' 'cmake')
checkdepends=('procps-ng' 'gdb')
options=('!emptydirs')
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml config.toml.bootstrap)
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
groups=('blfs' 'programming')

for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}")
    fi
done

prepare() {
    cd "rustc-$pkgver-src"
    if [[ ! "$(pacman -Qq "rust" 2>/dev/null)" ]];then
        cp "$srcdir"/config.toml.bootstrap config.toml
    else
        cp "$srcdir"/config.toml config.toml
    fi
}

build() {
    cd "rustc-$pkgver-src"

    [[ -z $MAKEFLAGS ]] && MAKEFLAGS='-j1'
    python ./x.py build "$MAKEFLAGS"
}

package_rust() {
    depends=('gcc-libs' 'llvm-libs' 'curl' 'libssh2')
    provides=('cargo' 'rustfmt')
    conflicts=('cargo' 'rustfmt')
    replaces=('cargo' 'rustfmt')

    cd "rustc-$pkgver-src"

    DESTDIR="$pkgdir" python ./x.py install

    for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
                                          "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

    cd "$pkgdir/usr/lib"

    rm rustlib/{components,manifest-rustc,rust-installer-version}
    ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

    # move docs out of the way for splitting
    mv "$pkgdir"/usr/share/doc "$srcdir"

    install -d "$pkgdir"/usr/share/bash-completion
    mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/
}

package_rust-docs() {
    install -d "$pkgdir/usr/share/doc/"
    mv "$srcdir"/doc/* "$pkgdir"/usr/share/doc/rust/

    msg2 "Packaging license files for the documentation"
    for license in APACHE MIT; do install -Dm644 "rustc-$pkgver-src/LICENSE-$license" \
                                          "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

md5sums=('e67432b37150f13e186ebfb67e5192fb'
         'SKIP'
         'bf9735c4c8b4accee74e4cc427ae0c68'
         '71edb3029b4608ca225ec2a234cc1ca8')
