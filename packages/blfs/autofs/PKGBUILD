#-*-mode:sh-*-
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Leonid Isaev
# Contributor: alexforsale@yahoo.com
pkgname=autofs
pkgver=5.1.5
pkgrel=1
pkgdesc='A kernel-based automounter for Linux'
arch=('x86_64')
url='https://www.kernel.org/pub/linux/daemons/autofs/'
license=('GPL2')
depends=('libxml2' 'libtirpc')
makedepends=('libldap' 'krb5' 'kmod' 'sssd' 'libnsl' 'rpcsvc-proto')
optdepends=('krb5: for LDAP support'
            'sssd: for SSSD integration')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs.conf'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
source=("https://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar."{xz,sign}
        "$pkgname.config")
validpgpkeys=('CD0A6E3CBB6768800B0736A8E7677380F54FD8A9') # Ian Kent
groups=('blfs' 'system-utilities')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
        -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
	        --sysconfdir=/etc/autofs \
	        --with-mapdir=/etc/autofs \
	        --with-confdir=/etc/default \
	        --without-hesiod \
	        --enable-ignore-busy \
	        --with-libtirpc \
	        --with-systemd
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make INSTALLROOT="${pkgdir}" install

    rm -rf "$pkgdir/run"
    install -dm755 "$pkgdir/etc/autofs/auto.master.d"

    install -vdm755 $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
    install -m644 ../$pkgname.config $pkgdir/usr/local/share/linuxfromscratch/kernel_configs

    make INSTALLROOT="${pkgdir}" install_samples
}

sha256sums=('23afdad9e2fd6d41b1900be8cfecfd5aae0ad174c7708ff685ec27e280f9c0da'
            'SKIP'
            '9a0cb4fa90c9de41a48020ea288c3350094572807851f7e7fc73cc6607aab836')
