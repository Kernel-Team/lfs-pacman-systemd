#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=libvdpau
pkgver=1.3
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
depends=('libxext')
license=('custom')
_commit=f57a9904c43ef5d726320c77baa91d0c38361ed4  # 1.3
source=("git+https://anongit.freedesktop.org/git/vdpau/libvdpau#commit=$_commit")
makedepends=('dri2proto' git)
sha256sums=('SKIP')
groups=('extra')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^libvdpau-//;s/-/+/g'
}

prepare() {
    meson-helper $pkgname build
}

check() {
    ninja -C build test
}

build() {
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    install -Dm 644 $pkgname/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
