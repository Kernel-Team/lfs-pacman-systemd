#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=gnupg
pkgver=2.2.17
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='https://www.gnupg.org/'
license=('GPL')
arch=('x86_64')
checkdepends=()
makedepends=()
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'bzip2' 'readline' 'gnutls' 'sqlite')
optdepends=('libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon'
            'pcsclite: scdaemon')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
install=install
conflicts=('dirmngr' 'gnupg2')
provides=('dirmngr' "gnupg2=${pkgver}")
replaces=('dirmngr' 'gnupg2')
groups=('blfs' 'security')

if [[ "$(pacman -Qq openssh 2>/dev/null)" ]];then
    checkdepends+=( 'openssh')
    _OPENSSH=1
fi
if [[ "$(pacman -Qq libldap 2>/dev/null)" ]];then
    makedepends+=( 'libldap')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'libldap: makedepends')
fi
if [[ "$(pacman -Qq libusb-compat 2>/dev/null)" ]];then
    makedepends+=( 'libusb-compat')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'libusb-compat: makedepends')
fi
if [[ "$(pacman -Qq pcsclite 2>/dev/null)" ]];then
    makedepends+=( 'pcsclite')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'pcsclite: makedepends')
fi

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-symcryptrun \
        --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    if [[ "${_OPENSSH}" == "1" ]];then
        make check || true
    fi
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    ln -s gpg "${pkgdir}"/usr/bin/gpg2
    ln -s gpgv "${pkgdir}"/usr/bin/gpgv2

    cd doc/examples/systemd-user
    for i in *.*; do
        install -Dm644 "$i" "${pkgdir}/lib/systemd/user/$i"
    done
}

md5sums=('1ba2d9b70c377f8e967742064c27a19c'
         'SKIP')
