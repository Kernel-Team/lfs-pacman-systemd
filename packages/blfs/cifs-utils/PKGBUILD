#-*-mode:sh-*-
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=cifs-utils
pkgver=6.9
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(x86_64)
url="https://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient' 'linux-pam')
makedepends=('python-docutils')
source=("https://download.samba.org/pub/linux-cifs/$pkgname/$pkgname-$pkgver.tar.bz2"{,.asc}
        "$pkgname.config")
validpgpkeys=('C699981A31F338706C817650DF5BA9D30642D5A0') #cifs-utils Distribution Verification Key <cifs-utils@samba.org>
groups=('blfs' 'networking-programs')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    # systemd support is broken in mount.cifs
    # https://bugs.archlinux.org/task/30958
    autoreconf -i
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    mkdir -p $pkgdir/etc/request-key.d
    install -m 644 contrib/request-key.d/cifs.idmap.conf $pkgdir/etc/request-key.d
    install -m 644 contrib/request-key.d/cifs.spnego.conf $pkgdir/etc/request-key.d
    # set mount.cifs uid, to enable none root mounting form fstab
    chmod +s $pkgdir/sbin/mount.cifs
    # fix idmap-plugin #42052
    mkdir -p $pkgdir/etc/cifs-utils
    ln -s /usr/lib/cifs-utils/idmapwb.so ${pkgdir}/etc/cifs-utils/idmap-plugin

    install -vdm755 $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
    install -m644 ../$pkgname.config $pkgdir/usr/local/share/linuxfromscratch/kernel_configs
}

md5sums=('6ae854852e39ab1d5e2e3b512fdffb08'
         'SKIP'
         '3d9b4214b9d5fb8fbaac8e1c708ee236')
