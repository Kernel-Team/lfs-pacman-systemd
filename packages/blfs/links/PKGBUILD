#-*-mode:sh-*-
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
pkgname=links
pkgver=2.20.1
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
_deps=('gpm' 'libevent' 'pcre')
depends=('bzip2' 'xz')
_makedeps=('libtiff' 'libpng' 'libxt' 'librsvg')
makedepends=()
optdepends=('libx11: for using xlinks'
	    'libtiff: for using xlinks'
	    'libpng: for using xlinks'
	    'librsvg: for using xlinks')
source=("http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2"
        'links.desktop')
groups=('blfs' 'text-web-browsers')

configure_options=(
  --prefix=/usr
  --mandir=/usr/share/man
  --disable-javascript
)

for _d in "${_deps[@]}";do
    if [[ "$(pacman -Qq "${_d}" 2>/dev/null)" ]];then
        depends+=( "${_d}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_d}: optional depend")
    fi
done
for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}: optional makedepends")
    fi
done

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}

build() {
    cd ${pkgname}-${pkgver}
    (cd intl; ./gen-intl; ./synclang)
    if [[ "$(pacman -Qq "${_makedeps[@]}" 2>/dev/null)" ]];then
        ./configure \
            "${configure_options[@]}" \
            --enable-graphics \
            --with-x \
            --with-fb
        make
        mv links xlinks
    fi
  ./configure \
    "${configure_options[@]}" \
    --disable-graphics \
    --without-x \
    --without-fb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  if [[ "$(pacman -Qq "${_makedeps[@]}" 2>/dev/null)" ]];then
      install -D -m0755 xlinks "${pkgdir}/usr/bin/xlinks"
      ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"
      install -D -m0644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
      install -d "${pkgdir}/usr/share/pixmaps"
      install -m0644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/usr/share/pixmaps/"
  fi

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m0644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}

sha256sums=('bb28a72cb72ca275742ef79e92ba468f0707863366bff2704b0ff6ce52790405'
            '484571172aa19cf91c4ce9d9528a4ac52e9252d6e741897e7339880aa30dbc88')
