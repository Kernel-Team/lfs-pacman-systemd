#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gstreamer
pkgver=1.16.0
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework core library"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 glib2 libunwind libcap libelf)
makedepends=(gtk-doc gobject-introspection git valgrind bash-completion meson)
checkdepends=(gmp gsl gtk3)
_commit=89c221a6972513f629c969f5485f37e77ccfa6e1  # tags/1.16.0^0
install=gstreamer.install
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer.git#commit=$_commit"
        "gst-common::git+https://gitlab.freedesktop.org/gstreamer/common.git")
sha256sums=('SKIP'
            'SKIP')
groups=('blfs' 'multimedia-libraries-drivers')

if [[ ! $(pacman -Qq gtk3 2>/dev/null) ]];then
   _confargs=(-D tests=disabled -D examples=disabled -D tools=disabled)
fi
   
pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $pkgname

    git submodule init
    git config --local submodule.common.url "$srcdir/gst-common"
    git submodule update

    cd ..
    meson-helper $pkgname build \
                 -Dgst_debug=false \
                 -D dbghelp=disabled \
                 -D gobject-cast-checks=disabled \
                 -D glib-asserts=disabled \
                 -D glib-checks=disabled \
                 -D package-name="GStreamer ${pkgver} BLFS" \
                 -D package-origin="http://www.linuxfromscratch.org/blfs/view/svn/" ${_confargs[@]}
}

build() {
    ninja -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
