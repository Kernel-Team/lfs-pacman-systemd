#-*-mode:sh-*-
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>
# Contributor: alexforsale@yahoo.com
pkgname=libevent
pkgver=2.1.11
pkgrel=1
pkgdesc="An event notification library"
arch=('x86_64')
url="http://libevent.org/"
license=('BSD')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
source=(https://github.com/libevent/libevent/releases/download/release-$pkgver-stable/$pkgname-$pkgver-stable.tar.gz{,.asc})
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA'
              '9E3AC83A27974B84D1B3401DB86086848EF8686D')
groups=('blfs' 'networking-libraries')

prepare(){
    cd ${pkgname}-${pkgver}-stable
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc --disable-libevent-regress
}

build() {
    cd ${pkgname}-${pkgver}-stable
    make
}

check() {
    cd ${pkgname}-${pkgver}-stable
    make -j1 check
}

package() {
    cd ${pkgname}-${pkgver}-stable
    make DESTDIR="${pkgdir}" install
    install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/libevent/LICENSE
}

sha256sums=('a65bac6202ea8c5609fd5c7e480e6d25de467ea1917c08290c521752f147283d'
            'SKIP')
