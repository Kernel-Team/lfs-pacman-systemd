#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=pcre
pkgver=8.43
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions'
arch=('x86_64')
url='http://www.pcre.org/'
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'bash')
groups=('blfs')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("https://ftp.pcre.org/pub/pcre/$pkgname-$pkgver.tar.bz2"{,.sig})
md5sums=('636222e79e392c3d95dcc545f24f98c4'
         'SKIP')

prepare() {
    cd $pkgname-$pkgver
    ./configure \
        --prefix=/usr \
        --docdir=/usr/share/doc/${pkgname}-${pkgver} \
        --enable-unicode-properties \
        --enable-pcre16 \
        --enable-pcre32 \
        --enable-jit \
        --enable-pcregrep-libz \
        --enable-pcregrep-libbz2 \
        --enable-pcretest-libreadline
}

build() {
    cd $pkgname-$pkgver
    make
}

check() {
    cd $pkgname-$pkgver
    make -j1 check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    install -v -dm755 "$pkgdir"/lib
    mv -v  "$pkgdir"/usr/lib/libpcre.so.*  "$pkgdir"/lib
    ln -sfv ../../lib/$(readlink  "$pkgdir"/usr/lib/libpcre.so)  "$pkgdir"/usr/lib/libpcre.so

    install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
