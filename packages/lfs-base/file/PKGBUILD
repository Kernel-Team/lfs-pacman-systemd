# -*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=file
pkgname=($pkgbase lib32-$pkgbase libx32-$pkgbase)
pkgver=5.37
pkgrel=1
arch=('x86_64')
license=('custom')
url='https://www.darwinsys.com/file/'
source=("ftp://ftp.astron.com/pub/$pkgbase/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A') # Christos Zoulas

prepare() {
  mkdir -pv build-{$pkgbase,lib{,x}32-$pkgbase}
  
  cd $srcdir/build-$pkgbase
  $srcdir/$pkgname-$pkgver/configure --prefix=/usr \
                                     --datadir=/usr/share/file \
                                     --enable-fsect-man5 \
                                     --disable-libseccomp
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  cd $srcdir/build-lib32-$pkgbase
  CC="gcc -m32 -march=i686" \
    CXX="g++ -m32 -march=i686" \
    PKG_CONFIG_PATH="/usr/lib32/pkgconfig" \
    $srcdir/$pkgname-$pkgver/configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-fsect-man5 \
    --disable-libseccomp \
    --host=i686-pc-linux-gnu
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  cd $srcdir/build-libx32-$pkgbase
  CC="gcc -mx32" \
    CXX="g++ -mx32" \
    PKG_CONFIG_PATH="/usr/libx32/pkgconfig" \
    $srcdir/$pkgname-$pkgver/configure --prefix=/usr \
    --libdir=/usr/libx32 \
    --enable-fsect-man5 \
    --disable-libseccomp \
    --host=x86_64-pc-linux-gnux32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
}

build() {
  cd $srcdir/build-$pkgbase
  make 

  cd $srcdir/build-lib32-$pkgbase
  make

  cd $srcdir/build-libx32-$pkgbase
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_file() {
  pkgdesc='utility for determining the type of a given file or files'
  depends=('glibc' 'zlib')

  cd $srcdir/build-$pkgbase
  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_lib32-file(){
  pkgdesc='utility for determining the type of a given file or files (32-bit)'
  depends=('lib32-glibc' "$pkgbase")
  groups=('lfs-multilib')

  cd $srcdir/build-lib32-$pkgbase
  make install DESTDIR=${pkgdir}
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $pkgbase "$pkgdir/usr/share/licenses/$pkgname"
}

package_libx32-file(){
  pkgdesc='utility for determining the type of a given file or files (x32 ABI)'
  depends=('libx32-glibc' "$pkgbase")
  groups=('lfs-multilib')

  cd $srcdir/build-libx32-$pkgbase
  make install DESTDIR=${pkgdir}
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $pkgbase "$pkgdir/usr/share/licenses/$pkgname"
}

md5sums=('80c29aca745466c6c24d11f059329075'
         'SKIP')
