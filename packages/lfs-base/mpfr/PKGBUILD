#-*-mode:sh-*-
# Contributor: alexforsale@yahoo.com
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>
pkgname=mpfr
pkgver=4.0.2
pkgrel=1
pkgdesc='Multiple-precision floating-point library'
arch=(x86_64)
url='http://www.mpfr.org/'
license=(LGPL)
depends=('gmp>=5.0')
groups=('lfs-base')
source=(http://www.mpfr.org/mpfr-$pkgver/mpfr-${pkgver}.tar.xz{,.asc})
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

prepare(){
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --disable-static \
                --enable-thread-safe \
                --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
    make check-exported-symbols
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}

md5sums=('320fbc4463d4c8cb1e566929d8adc4f8'
         'SKIP')
