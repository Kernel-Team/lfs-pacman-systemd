#-*-mode:sh-*-
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: alexforsale@yahoo.com
pkgname=gawk
pkgver=5.0.1
pkgrel=1
pkgdesc="GNU version of awk"
arch=('x86_64')
url="http://www.gnu.org/software/gawk/"
license=('GPL')
depends=('sh' 'glibc' 'mpfr')
provides=('awk')
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('c5441c73cc451764055ee65e9a4292bb'
         'SKIP')
groups=('lfs-base')
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2') # Arnold Robbins

prepare(){
    cd ${srcdir}/${pkgname}-${pkgver}

    # ensure some unneeded files are not installed
    sed -i 's/extras//' Makefile.in
    
    ./configure --prefix=/usr --sysconfdir=/etc --without-libsigsegv
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
