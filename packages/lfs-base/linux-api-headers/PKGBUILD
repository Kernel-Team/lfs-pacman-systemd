#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=linux-api-headers
pkgver=5.2.9
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
url="http://www.gnu.org/software/libc"
arch=('any')
license=('GPL2')
source=(https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${pkgver}.tar.{xz,sign})
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman
groups=('lfs-base')

build() {
    cd linux-$pkgver

    make mrproper
    make headers_check
}

package() {
    cd linux-$pkgver
    make INSTALL_HDR_PATH="$pkgdir/usr" headers_install

    # clean-up unnecessary files generated during install
    find "$pkgdir" \( -name .install -o -name ..install.cmd \) -delete
}

md5sums=('d7162b4421327c756b1d548204fd8fb1'
         'SKIP')
