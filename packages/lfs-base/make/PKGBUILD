#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=make
pkgver=4.2.1
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
depends=('glibc')
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
validpgpkeys=('3D2554F0A15338AB9AF1BB9D96B047156338B6D4')   # Paul Smith
groups=('lfs-base')

if [[ "$(pacman -Qq guile 2>/dev/null)" ]];then
    depends+=( 'guile')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'guile')
fi

prepare() {
    cd ${pkgname}-${pkgver}

    # work around an error caused by glibc-2.27
    sed -i '211,217 d; 219,229 d; 232 d' glob/glob.c

    ./configure --prefix=/usr
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make -k check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
md5sums=('15b012617e7c44c0ed482721629577ac'
         'SKIP')
