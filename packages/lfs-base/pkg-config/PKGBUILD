#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=pkg-config
pkgver=0.29.2
pkgrel=1
pkgdesc="A system for managing library compile/link flags"
url="https://www.freedesktop.org/wiki/Software/pkg-config/"
arch=('x86_64')
license=('GPL2')
depends=('glibc')
provides=('pkgconfig')
conflicts=('pkgconfig')
groups=('lfs-base')
source=("https://pkg-config.freedesktop.org/releases/pkg-config-$pkgver.tar.gz"{,.asc}
        0001-Workaround-error-m4_copy.patch)
validpgpkeys=('6B99CE97F17F48C27F722D71023A4420C7EC6914')

_confargs=( '--with-pc-path=/usr/lib/pkgconfig:/usr/lib64/pkgconfig:/usr/local/lib/pkgconfig:/usr/local/lib64/pkgconfig:/usr/share/pkgconfig:/usr/local/share/pkgconfig')

if [[ "$(pacman -Qq glib2 2>/dev/null)" ]];then
    depends+=( 'glib2')
    _confargs=('--without-internal-glib')
    pkgrel=$((pkgrel+1))
else
    _confargs=('--with-internal-glib')
    optdepends+=( 'glib2')
fi

if [[ "$(pacman -Qq automake 2>/dev/null)" ]];then
    makedepends+=( 'automake')
    pkgrel=$((pkgrel+1))
fi

prepare() {
    cd $pkgname-$pkgver
    if [[ $(pacman -Qq automake 2>/dev/null) ]];then
        patch -Np1 -i "$srcdir/0001-Workaround-error-m4_copy.patch"
        autoreconf -fvi
    fi
}

build() {
    cd $pkgname-$pkgver
    ./configure --prefix="/usr" \
                --docdir=/usr/share/doc/$pkgname-$pkgver \
                ${_confargs[@]}
    make
}

check() {
    cd $pkgname-$pkgver
    make check || true
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}

md5sums=('f6e931e319531b736fadc017f470e68a'
         'SKIP'
         '3d82f3cc343b636c71e35715bab24d32')
