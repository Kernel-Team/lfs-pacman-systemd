#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=bison
pkgver=3.4.1
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('glibc' 'm4')
groups=('lfs-base')
source=("ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz"{,.sig})
validpgpkeys=('7DF84374B1EE1F9764BBE25D0DDCAA3278D5264E')  # Akim Demaille

if [[ "$(pacman -Ss ^sh$ 2>/dev/null)" ]];then
    depends+=( 'sh')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'sh')
fi

prepare(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '6855 s/mv/cp/' Makefile.in
    ./configure --prefix=/usr --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make -j1 check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

md5sums=('201286a573b12da109df96282fe4ff4a'
         'SKIP')
