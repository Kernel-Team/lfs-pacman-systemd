#-*-mode:sh-*-
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=bash
_basever=5.0
_patchlevel=011
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc='The GNU Bourne Again shell'
arch=(x86_64)
license=(GPL)
url='http://www.gnu.org/software/bash/bash.html'
backup=(etc/bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=7.0' 'glibc' 'ncurses')
optdepends=('bash-completion: for tab completion')
provides=('sh')
groups=('lfs-base')
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout
        bash_completion.sh
       )
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
    for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

prepare() {
    cd $pkgname-$_basever

    for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
        msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
        patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
    done

    _bashconfig=(-DSYS_BASHRC=\'\"/etc/bashrc\"\'
                 -DSYS_BASH_LOGOUT=\'\"/etc/bash_logout\"\'
                 -DNON_INTERACTIVE_LOGIN_SHELLS)
    export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

    ./configure --prefix=/usr --without-bash-malloc --with-installed-readline \
                --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

build() {
    cd $pkgname-$_basever
    make
}

check() {
    make -C $pkgname-$_basever check
}

package() {
    make -C $pkgname-$_basever DESTDIR="$pkgdir" install

    install -v -dm755 "$pkgdir"/bin
    mv -vf "$pkgdir"/usr/bin/bash "$pkgdir"/bin
    ln -s bash "$pkgdir/bin/sh"
    ln -s ../../bin/bash "$pkgdir/usr/bin/sh"
    ln -s ../../bin/bash "$pkgdir/usr/bin/bash"

    # system-wide configuration files
    install -v -Dm644 system.bashrc "$pkgdir/etc/bashrc"
    install -v -Dm644 system.bash_logout "$pkgdir/etc/bash_logout"
    install -v -dm755 "$pkgdir/etc/bash_completion.d/"
    install -v -dm755 "$pkgdir/etc/profile.d/"
    install -v -Dm644 bash_completion.sh "$pkgdir/etc/profile.d/bash_completion.sh"
    
    # user configuration file skeletons
    install -v -dm755 "$pkgdir/etc/skel/"
    install -v -m644 dot.bashrc "$pkgdir/etc/skel/.bashrc"
    install -v -m644 dot.bash_profile "$pkgdir/etc/skel/.bash_profile"
    install -v -m644 dot.bash_logout "$pkgdir/etc/skel/.bash_logout"
}

md5sums=('2b44b47b905be16f45709648f671820b'
         'SKIP'
         '646ca28ca35dffaed560924a2ba3deb0'
         '0bca0019c23df318e21a0326ba81af17'
         'f70bbec388eac3b99a99718b96b6d4c6'
         'aa3b01968fe25e4b942a7ad3e3d47c51'
         '18f5d7f471486927931da64c06da809c'
         'bd1247f39c4990ae1d1a163b4a93f280'
         'b026862ab596a5883bb4f0d1077a3819'
         'SKIP'
         '2f4a7787365790ae57f36b311701ea7e'
         'SKIP'
         'af7f2dd93fd5429fb5e9a642ff74f87d'
         'SKIP'
         'b60545b273bfa4e00a760f2c648bed9c'
         'SKIP'
         '875a0bedf48b74e453e3997c84b5d8a4'
         'SKIP'
         '4a8ee95adb72c3aba03d9e8c9f96ece6'
         'SKIP'
         '411560d81fde2dc5b17b83c3f3b58c6f'
         'SKIP'
         'dd7cf7a784d1838822cad8d419315991'
         'SKIP'
         'c1b3e937cd6dccbb7fd772f32812a0da'
         'SKIP'
         '19b41e73b03602d0e261c471b53e670c'
         'SKIP'
         '414339330a3634137081a97f2c8615a8'
         'SKIP')
