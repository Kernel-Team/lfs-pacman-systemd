#-*-mode:sh-*-
# Maintainer: %s <%s>
pkgname=iproute2
pkgver=5.2.0
pkgrel=1
pkgdesc='IP Routing Utilities'
arch=('x86_64')
license=('GPL2')
url='https://git.kernel.org/pub/scm/network/iproute2/iproute2.git'
depends=('glibc'
         'libelf')
optdepends=('linux-atm: ATM support')
provides=('iproute')
backup=('etc/iproute2/ematch_map'
        'etc/iproute2/rt_dsfield'
        'etc/iproute2/rt_protos'
        'etc/iproute2/rt_realms'
        'etc/iproute2/rt_scopes'
        'etc/iproute2/rt_tables')
makedepends=()
options=('staticlibs')
validpgpkeys=('9F6FC345B05BE7E766B83C8F80A77F6095CDE47E') # Stephen Hemminger
source=("https://www.kernel.org/pub/linux/utils/net/${pkgname}/${pkgname}-${pkgver}.tar."{xz,sign}
        '0001-make-iproute2-fhs-compliant.patch')
groups=('lfs-base')

if [[ "$(pacman -Qq iptables 2>/dev/null)" ]];then
    depends+=( 'iptables')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'iptables')
fi
if [[ "$(pacman -Qq linux-atm 2>/dev/null)" ]];then
    makedepends+=( 'linux-atm')
else
    optdepends+=( 'linux-atm')
fi

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # set correct fhs structure
    patch -Np1 -i "${srcdir}/0001-make-iproute2-fhs-compliant.patch"

    # do not treat warnings as errors
    sed -i 's/-Werror//' Makefile

    ./configure
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # libnetlink isn't installed, install it FS#19385
    install -Dm0644 include/libnetlink.h "${pkgdir}/usr/include/libnetlink.h"
    install -Dm0644 lib/libnetlink.a "${pkgdir}/usr/lib/libnetlink.a"
}

md5sums=('0cb2736e7bc2f56254a363d3d23703b7'
         'SKIP'
         '5345bd18b521fb2f305acb2ab1203269')
