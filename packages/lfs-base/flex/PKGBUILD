#-*-mode:sh-*-
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: alexforsale@yahoo.com
pkgname=flex
pkgver=2.6.4
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('x86_64')
url="https://github.com/westes/flex"
license=('custom')
depends=('glibc' 'm4')
makedepends=()
groups=('lfs-base')
source=("https://github.com/westes/flex/releases/download/v$pkgver/flex-$pkgver.tar.gz")
md5sums=('2882e3179748cc9f9c23ec593d6adc8d')

if [[ "$(pacman -Qq sh 2>/dev/null)" ]];then
    depends+=( 'sh')
    pkgrel=$((pkgrel+1))
else
    optdepends+=( 'sh')
fi
if [[ "$(pacman -Qq help2man 2>/dev/null)" ]];then
    makedepends+=( 'help2man')
else
    optdepends+=( 'help2man')
fi

prepare(){
    cd "$pkgname-$pkgver"

    # fix a problem introduced with glibc-2.26
    sed -i "/math.h/a #include <malloc.h>" src/flexdef.h

    [[ ! $(command -v help2man) ]] && HELP2MAN=/tools/bin/true 
    ./configure --prefix=/usr --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

build() {
    cd "$pkgname-$pkgver"
    make
}

check() {
    cd "$pkgname-$pkgver"

    # cxx_restart fails - https://github.com/westes/flex/issues/98
    make -k check || true
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    ln -s flex "${pkgdir}/usr/bin/lex"

    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
