#-*-mode:sh-*-
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Link Dupont <link@subpop.net>
# Contributor: alexforsale@yahoo.com
pkgbase=dbus
pkgname=(dbus dbus-docs)
pkgver=1.12.16
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
depends=('libsystemd' 'expat')
_makedeps=('xmlto' 'docbook-xsl' 'yelp-tools' 'doxygen' 'graphviz')
makedepends=(systemd
             python
            )
_commit=d4f8423bbff9b3c5fca2d8009c28d1cff4652788  # tags/dbus-1.12.12^0
source=(https://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz{,.asc}
        'dbus.sysusers')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>
groups=('lfs-base')

for _d in ${_makedeps[@]};do
    if [[ "$(pacman -Qq "${_d[@]/#/python-}" 2>/dev/null)" ]];then
        makedepends+=( "${_d[@]}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_d}")
    fi
done

prepare() {
    cd $pkgbase-$pkgver

    # Reduce docs size
    printf '%s\n' >>Doxyfile.in \
           HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes

    _confargs=()
    if [[ "$(pacman -Qq doxygen 2>/dev/null)" ]];then
        _confargs+=( '--enable-doxygen-docs')
    else
        _confargs+=( '--disable-doxygen-docs')
    fi

    if [[ "$(pacman -Qq xmlto 2>/dev/null)" ]] &&
           [[ "$(pacman -Qq docbook-xsl 2>/dev/null)" ]];then
        _confargs+=( '--enable-xml-docs')
    else
        _confargs+=( '--disable-xml-docs')
    fi
    
    #NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --disable-static \
                --with-dbus-user=dbus \
                --with-system-pid-file=/run/dbus/pid \
                --with-system-socket=/run/dbus/system_bus_socket \
                --with-console-auth-dir=/run/console/ \
                --enable-inotify \
                --disable-verbose-mode \
                --disable-asserts \
                --with-systemdsystemunitdir=/lib/systemd/system \
                --enable-systemd \
                --enable-user-session \
                --docdir=/usr/share/doc/${pkgname}-${pkgver} ${_confargs[@]}
}

build() {
    cd $pkgbase-$pkgver
    make
}

check() {
    cd $pkgbase-$pkgver
    make check
}

package_dbus() {
    provides=(libdbus)
    conflicts=(libdbus)
    replaces=(libdbus)

    cd $pkgbase-$pkgver

    make DESTDIR="$pkgdir" install

    install -v -dm755 "$pkgdir"/lib
    
    mv -v "$pkgdir"/usr/lib/libdbus-1.so.* "$pkgdir"/lib
    ln -sfv ../../lib/$(readlink "$pkgdir"/usr/lib/libdbus-1.so) "$pkgdir"/usr/lib/libdbus-1.so

    ln -sfv  ../../../etc/machine-id "$pkgdir"/var/lib/dbus

    rm -r "$pkgdir/var/run"

    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgbase/COPYING"

    # We have a pre-assigned uid (81)
    install -m644 "$srcdir/dbus.sysusers" "$pkgdir/usr/lib/sysusers.d/dbus.conf"

    # Split docs
    mv "$pkgdir/usr/share/doc" "$srcdir"

    install -vdm755 ${pkgdir}/lib/systemd
    mv -v ${pkgdir}/usr/lib/systemd/* ${pkgdir}/lib/systemd/
    rm -rfv ${pkgdir}/usr/lib/systemd

}

package_dbus-docs() {
    pkgdesc+=" (documentation)"
    depends=(dbus)

    install -d "$pkgdir/usr/share/licenses"
    ln -s dbus "$pkgdir/usr/share/licenses/dbus-docs"

    mv doc "$pkgdir/usr/share"
}

md5sums=('2dbeae80dfc9e3632320c6a53d5e8890'
         'SKIP'
         'c627e77fb41151ec5d4cd6c24b256f5d')
