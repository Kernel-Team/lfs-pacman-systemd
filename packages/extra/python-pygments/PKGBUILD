#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=('python-pygments' 'python2-pygments' 'pygmentize')
pkgver=2.4.2
pkgrel=1
pkgdesc="Python syntax highlighter"
arch=('any')
url="http://pygments.org/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=(https://pypi.org/packages/source/P/Pygments/Pygments-$pkgver.tar.gz)
groups=('extra')

package_python-pygments() {
    groups+=( 'python-extra')
    depends=('python-setuptools')

    cd "$srcdir/Pygments-$pkgver"

    python3 setup.py install --root="$pkgdir" -O1

    # pygmentize is shipped in its own package
    rm "$pkgdir/usr/bin/pygmentize"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pygments() {
    depends=('python2-setuptools')
    groups+=( 'python-extra')
    install=python2-pygments.install

    cd "$srcdir/Pygments-$pkgver"

    python2 setup.py install --root="$pkgdir" -O1

    # pygmentize is shipped in its own package
    rm "$pkgdir/usr/bin/pygmentize"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pygmentize() {
    depends=('python-pygments')

    cd "$srcdir/Pygments-$pkgver"

    python3 setup.py install --root="$pkgdir" -O1

    # Remove all files except for usr/bin/pygmentize
    find "$pkgdir" -type f -not -name pygmentize -delete

    # Drop version dependency from console script
    sed -i "s/Pygments==$pkgver/Pygments/g" "$pkgdir/usr/bin/pygmentize"

    install -Dm644 external/pygments.bashcomp \
            "$pkgdir/usr/share/bash-completion/completions/pygmentize"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('08857f5e15d6b4badb8bea96fc7aaf3a02a504f9b87171e01c6ace25250d254691c64661dca8e723824cabac4753c59ffa667ac0b6042506b05e4357389e658c')
