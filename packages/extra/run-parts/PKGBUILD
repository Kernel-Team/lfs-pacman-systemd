#-*-mode:sh-*-
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: alexforsale@yahoo.com
pkgname=run-parts
pkgver=4.8.6.1
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('x86_64')
url='https://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("http://http.debian.net/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.xz")
sha256sums=('099f1e8a7278b26145a2ba2dda84c4118403bfab38c8d7070a6235a7ffcb55ed')
groups=('extra')

prepare(){
	cd $srcdir/debianutils
	./configure --prefix=/usr/local
}

build() {
	cd $srcdir/debianutils
	make run-parts
}

package() {
	cd $srcdir/debianutils
	install -D -m755 run-parts $pkgdir/usr/local/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/local/share/man/man8/run-parts.8
	for l in po4a/*/run-parts.8; do
		install -D -m644  ${l} $pkgdir/usr/local/share/man/${l:5:2}/man8/run-parts.8
	done
}
