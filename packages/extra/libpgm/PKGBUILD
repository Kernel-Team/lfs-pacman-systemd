#-*-mode:sh-*-
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Vladimir Kirillov <proger@wilab.org.ua>
pkgname=libpgm
pkgver=5.2.122
pkgrel=1
pkgdesc="OpenPGM: implementation of the Pragmatic General Multicast (PGM, RFC3208)"
arch=('x86_64')
url='https://www.freshports.org/net/openpgm/'
license=('LGPL2.1')
depends=('glibc')
makedepends=('python2')
options=(!strip)
#source=("http://openpgm.googlecode.com/files/$pkgname-$pkgver.tar.gz")
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/openpgm/libpgm-$pkgver.tar.gz")
sha256sums=('6b895f550b95284dcde7189b01e04a9a1c1f94579af31b1eebd32c2207a1ba2c')
groups=('extra')

prepare() {
    cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
    find . -type f -exec sed -i 's/python/python2/g' {} \+
    ./configure --prefix=/usr
}

build() {
    cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
    make prefix="$pkgdir/usr" install
}
