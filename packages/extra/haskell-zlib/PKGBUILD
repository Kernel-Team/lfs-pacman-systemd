#-*-mode:sh-*-
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Colin Woodbury <colingw@gmail.com>
# Contributor: alexforsale@yahoo.com
pkgname=haskell-zlib
pkgver=0.6.2.1
pkgrel=1
pkgdesc="Compression and decompression in the gzip and zlib formats"
url="https://hackage.haskell.org/package/zlib"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'zlib')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/zlib/${pkgver}/zlib-${pkgver}.tar.gz)
sha512sums=('cee7942a86afbd08f03c19abd88154cc866a3ccc52342264d4d5b28a71643adf30bf03ba939d6a98b7c065752f9ec326729fc38cfe72b66998551c89186048c5')

build() {
    cd "$srcdir"/zlib-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
               --prefix=/usr/local --docdir=/usr/local/share/doc/$pkgname \
               --dynlibdir=/usr/local/lib --libsubdir=\$compiler/site-local/\$pkgid \
               -f-use-bytestring-builder -f-non-blocking-ffi -fpkg-config
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/zlib-${pkgver}"
    
    install -D -m744 register.sh "$pkgdir"/usr/local/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/local/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
    rm -f "$pkgdir"/usr/local/share/doc/${pkgname}/LICENSE
}
