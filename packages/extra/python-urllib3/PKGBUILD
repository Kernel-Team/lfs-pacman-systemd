#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=python-urllib3
pkgname=(python-urllib3 python2-urllib3)
pkgver=1.25.3
pkgrel=1
pkgdesc="HTTP library with thread-safe connection pooling and file post support"
arch=("any")
url="https://github.com/shazow/urllib3"
license=("MIT")
_makedeps=('python2-sphinx')
makedepends=('python-setuptools' 'python2-setuptools' 'python-ndg-httpsclient'
             'python2-ndg-httpsclient' 'python-pyasn1' 'python2-pyasn1' 'python-pyopenssl'
             'python2-pyopenssl' 'python-pysocks' 'python2-pysocks' 'python-mock' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-tornado' 'python2-tornado'
              'python-nose' 'python2-nose' 'python-psutil' 'python2-psutil'
              'python-gcp-devrel-py-tools' 'python2-gcp-devrel-py-tools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/shazow/urllib3/archive/$pkgver.tar.gz")
sha512sums=('86556d486c5aac9c3c7875d1440317c3be3924f67f4377a99e15892a39f532330d07f9dec5c5a1ed8b20957d2c611e78a97bd9d1684acff3069cd7debbbaa001')
groups=('extra' 'python-extra')

for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        depends+=( "${_m}")
        pkgname+=( 'python-urllib3-doc')
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}")
    fi
done

prepare() {
    cp -a urllib3-$pkgver{,-py2}
}

build() {
    cd "$srcdir"/urllib3-$pkgver
    python setup.py build

    cd "$srcdir"/urllib3-$pkgver-py2
    python2 setup.py build

    if [[ -n "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        # Build with Python 2 since autodoc produces errors on Python 3
        cd "$srcdir"/urllib3-$pkgver/docs
        make SPHINXBUILD=sphinx-build2 html
    fi
}

check() {
    # Tests do not support Tornado 5

    cd "$srcdir"/urllib3-$pkgver
    python setup.py pytest || warning "Tests failed"

    cd "$srcdir"/urllib3-$pkgver-py2
    python2 setup.py pytest || warning "Tests failed"
}

package_python-urllib3() {
    depends=('python')
    optdepends=('python-pysocks: SOCKS support')

    cd urllib3-$pkgver
    python setup.py install --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-urllib3() {
    depends=('python2')
    optdepends=('python2-pysocks: SOCKS support')

    cd urllib3-$pkgver-py2
    python2 setup.py install --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python-urllib3-doc() {
    pkgdesc="urllib3 Documentation"

    cd urllib3-$pkgver/docs
    install -d "$pkgdir"/usr/share/doc
    cp -r _build/html "$pkgdir"/usr/share/doc/python-urllib3
    install -Dm644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
