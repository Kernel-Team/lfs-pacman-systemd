#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Xavier D. <magicrhesus@ouranos.be>
# Contributor: Zarra <zarraxx@gmail.com>
pkgname=dbus-c++
_pkgname="lib${pkgname}"
_upstream="dbus-cplusplus"
pkgver=0.9.0
pkgrel=8
pkgdesc="A C++ API for D-BUS"
url="http://dbus-cplusplus.sourceforge.net/"
arch=('x86_64')
license=('LGPL')
depends=('dbus' 'glib2')
source=("https://sourceforge.net/projects/${_upstream}/files/${pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'gcc47.patch'
        'disable-threading.patch'
        'fix-writechar.patch')
groups=('extra')

prepare() {
    cd "${_pkgname}-${pkgver}"
    patch -Np0 -i "${srcdir}/gcc47.patch"
    patch -Np1 -i "${srcdir}/disable-threading.patch"
    patch -Np1 -i "${srcdir}/fix-writechar.patch"
    autoreconf -vi
}

build() {
    cd "${_pkgname}-${pkgver}"

    export LDFLAGS+=" -lexpat -lpthread" # -lpthread needed for i686

    ./configure --prefix=/usr \
                --disable-static \
                --disable-ecore \
                --enable-glib
    make
}

package() {
    cd "${_pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install
    install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
    install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
    install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
    install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"

}

sha512sums=('7acebbb4254b2886cc0f05c5ddeeeac0b5863c5552d32249463b89380b0b95b8225c80bd98b8c7fcaada42ab770b5eff41b15390cd0d78bf1ee322ac6c2de319'
            '249f43f2e08ded7d195732389ff01ec53e592769d01d8528614cefd780b6dbd283f9e0f6f9cfac88a848a7ba5d73c4bc6b7aae43513c8414c35067f38575f3ea'
            '94d95089ea84af1438191285955bf0866a7d7cbe535ac7fb8282ef6fb96c4ee052f439fb8c9d0b18b9ebec65d39cc45754bb48192ffb46c3d0c4ed6b1b6693a6'
            'df0a3a80d139877eaea5f352da5dfa2bc0407360ddfb59301707c77bd4329ecd986f366f27bbdd7e4ce385125c6ec46fcf115854aa01d97cf11dff905cd9a385')
