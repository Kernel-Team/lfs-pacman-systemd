#-*-mode:sh-*-
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=ppp
pkgver=2.4.7
pkgrel=1
pkgdesc="A daemon which implements the Point-to-Point Protocol for dial-up networking"
arch=(x86_64)
url="https://www.samba.org/ppp/"
license=('GPL' 'BSD')
depends=('glibc' 'libpcap>=1.0.0' 'openssl')
backup=(etc/ppp/{chap-secrets,pap-secrets,options,ip-up,ip-down,ip-down.d/00-dns.sh,ip-up.d/00-dns.sh,ipv6-up.d/00-iface-config.sh})
source=(https://download.samba.org/pub/ppp/ppp-${pkgver}.tar.gz{,.asc}
        ppp-2.4.6-makefiles.patch
        options
        ip-up
        ip-down
        ip-up.d.dns.sh
        ip-down.d.dns.sh
        ipv6-up
        ipv6-down
        ipv6-up.d.iface-config.sh
        ppp.systemd
        CVE-2015-3310.patch
        ppp-build-fix.patch::"https://github.com/paulusmack/ppp/commit/50a2997b.patch"
        ppp-openssl.patch::https://github.com/paulusmack/ppp/commit/3c7b86229f7bd2600d74db14b1fe5b3896be3875.patch
        LICENSE)
validpgpkeys=('631E179E370CD727A7F2A33A9E4295D605F66CE9') # Paul Mackerras (Signing key) <paulus@samba.org>
groups=('extra')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -p1 -i "${srcdir}/ppp-2.4.6-makefiles.patch"
    patch -p1 -i "${srcdir}/CVE-2015-3310.patch"
    patch -p1 -i "${srcdir}/ppp-build-fix.patch"
    patch -p1 -i "${srcdir}/ppp-openssl.patch"

    # enable active filter
    sed -i "s:^#FILTER=y:FILTER=y:" pppd/Makefile.linux
    # enable ipv6 support
    sed -i "s:^#HAVE_INET6=y:HAVE_INET6=y:" pppd/Makefile.linux
    # Enable Microsoft proprietary Callback Control Protocol
    sed -i "s:^#CBCP=y:CBCP=y:" pppd/Makefile.linux 
    # -D_GNU_SOURCE is needed for IPv6 to work apparently
    CFLAGS="$CPPFLAGS $CFLAGS -D_GNU_SOURCE" LDFLAGS="$LDFLAGS" ./configure --prefix=/usr
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make 
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make INSTROOT="${pkgdir}" install 

    install -D -m644 ../options "${pkgdir}/etc/ppp/options"
    install -D -m755 ../ip-up   "${pkgdir}/etc/ppp/ip-up"
    install -D -m755 ../ip-down "${pkgdir}/etc/ppp/ip-down"
    install -d -m755 "${pkgdir}/etc/ppp/ip-up.d"
    install -d -m755 "${pkgdir}/etc/ppp/ip-down.d"
    install -m755 ../ip-up.d.dns.sh   "${pkgdir}/etc/ppp/ip-up.d/00-dns.sh"
    install -m755 ../ip-down.d.dns.sh "${pkgdir}/etc/ppp/ip-down.d/00-dns.sh"
    install -D -m755 ../ipv6-up   "${pkgdir}/etc/ppp/ipv6-up"
    install -D -m755 ../ipv6-down "${pkgdir}/etc/ppp/ipv6-down"
    install -d -m755 "${pkgdir}/etc/ppp/ipv6-up.d"
    install -d -m755 "${pkgdir}/etc/ppp/ipv6-down.d"
    install -m755 ../ipv6-up.d.iface-config.sh "${pkgdir}/etc/ppp/ipv6-up.d/00-iface-config.sh"

    install -D -m755 scripts/pon  "${pkgdir}/usr/bin/pon"
    install -D -m755 scripts/poff "${pkgdir}/usr/bin/poff"
    install -D -m755 scripts/plog "${pkgdir}/usr/bin/plog"
    install -D -m644 scripts/pon.1 "${pkgdir}/usr/share/man/man1/pon.1"
    install -D -m600 etc.ppp/pap-secrets  "${pkgdir}/etc/ppp/pap-secrets"
    install -D -m600 etc.ppp/chap-secrets "${pkgdir}/etc/ppp/chap-secrets"
    install -d -m755 "${pkgdir}/etc/ppp/peers" 
    chmod 0755 "${pkgdir}/usr/lib/pppd/${pkgver}"/*.so

    install -D -m644 "${srcdir}"/ppp.systemd "${pkgdir}"/lib/systemd/system/ppp@.service

    install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

md5sums=('78818f40e6d33a1d1de68a1551f6595a'
         'SKIP'
         '3c0d6260d3c2e794c3751d52b4cf9d0c'
         '7a9259a8f038073eeea7e4552ff1849f'
         '529b924f644f80cf30d72a966abc7419'
         '529b924f644f80cf30d72a966abc7419'
         '0e2eeb1af125a46291999162dcc9004a'
         '3a5d56ac23e36cdbf48187813c8ec981'
         '4cbd0a9af01c27067450ec7d3f5fa673'
         'b75c2fbc4afa9f2d8108b680c88c7036'
         '9dde53aa5a931ebe1c7e3469199777c5'
         '37401eb0c3dcccb87f5ac4716d0f9f2c'
         'e71ba6426cf0c9d2e73d40f19508d35c'
         'b2b2161a8e93ecfac658f845510a082e'
         '57717b156d53ba13cbfe676771c4b480'
         'aee7726dd4d7c2ac881859e6f1992e00')
