#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=bash-completion
pkgver=2.9
pkgrel=1
pkgdesc='Programmable completion for the bash shell'
arch=(any)
url='https://github.com/scop/bash-completion'
license=(GPL2)
depends=(bash)
options=(!emptydirs !makeflags)
groups=('extra')
source=($url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
validpgpkeys=(A558062B6D5DA1347CD468D52B0AE2C1B09B07CB)
md5sums=('60139333b25c4d54ad40323b701be130')

prepare(){
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc
}

build() {
    cd $pkgname-$pkgver
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
    rm "$pkgdir/etc/profile.d/bash_completion.sh"

    # remove Slackware's makepkg completion
    rm "$pkgdir/usr/share/bash-completion/completions/makepkg"
}
