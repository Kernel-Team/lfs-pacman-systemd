#-*-mode:sh-*-
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: alexforsale@yahoo.com
pkgname=pacman
pkgver=5.1.3
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
_deps=('libarchive' 'curl' 'gpgme')
depends=('glibc' 'bash')
_makedeps=('asciidoc')
makedepends=()
_checkdeps=('python2' 'fakechroot')
checkdepends=()
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('strip')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf
        makepkg.conf)
groups=('extra')

for _c in "${_checkdeps[@]}";do
    if [[ "$(pacman -Qq "${_c}" 2>/dev/null)" ]];then
        checkdepends+=( "${_c}")
    else
        optdepends+=( "${_c}: optional checkdepend")
    fi
done
for _d in "${_deps[@]}";do
    if [[ "$(pacman -Qq "${_d}" 2>/dev/null)" ]];then
        depends+=( "${_d}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_d}: optional depend")
    fi
done
for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
        pkgrel=$((pkgrel+1))
    else
        optdepends+=( "${_m}: optional makedepends")
    fi
done

build() {
    cd "$pkgname-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc \
                --localstatedir=/var --enable-doc \
                --with-scriptlet-shell=/bin/bash \
                --with-ldconfig=/sbin/ldconfig
    make V=1
}

check() {
    make -C "$pkgname-$pkgver" check
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install

    # install Arch specific stuff
    install -dm755 "$pkgdir/etc"
    install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
    install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"

    # put bash_completion in the right location
    install -dm755 "$pkgdir/usr/share/bash-completion/completions"
    mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
    rmdir "$pkgdir/etc/bash_completion.d"

    for f in makepkg pacman-key; do
        ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
    done
    chmod -v 755 "$pkgdir"/usr/bin/*
}

md5sums=('cafd8e5b8b16863ee7a7579ec8402582'
         'SKIP'
         '289a5b4de0ebea82b035bc4af7866a2a'
         '6f59910b8f2c0441a71b26646b2f40f0')
