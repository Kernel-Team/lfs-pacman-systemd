#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=python-setuptools
pkgname=('python-setuptools' 'python2-setuptools')
pkgver=41.2.0
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="https://pypi.org/project/setuptools/"
_deps=('appdirs' 'packaging' 'pip')
makedepends=('python-six' 'python2-six')
_checkdeps=('mock' 'pip' 'pytest-fixture-config' 'pytest-flake8'
            'pytest-runner' 'pytest-virtualenv' 'wheel')
checkdepends=()
groups=('python-extra')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/setuptools/archive/v$pkgver.tar.gz")
sha512sums=('9ddaae9d530cb5580566abba4b9aec19d5bbbc9bd571eb121ce597d311e99c8f766fd905d856419355fa9a676040c41703c0d7fbe7e6d068e701dd89df4280e1')
groups=('extra' 'python-extra')

if [[ "$(pacman -Ss ^git$ 2>/dev/null)" ]];then
    makedepends+=( 'git')
    checkdepends+=( 'git')
fi
for _d in ${_deps[@]};do
    if [[ -n "$(pacman -Ss ^"${_d[@]/#/python-}"$ 2>/dev/null)" ]];then
        makedepends+=( "${_d[@]/#/python-}")
        depends+=( "${_d[@]/#/python-}")
    fi
    if [[ -n "$(pacman -Ss ^"${_d[@]/#/python2-}"$ 2>/dev/null)" ]];then
        makedepends+=( "${_d[@]/#/python2-}")
        depends+=( "${_d[@]/#/python2-}")
    fi
done
for _c in ${_checkdeps[@]};do
    if [[ -n "$(pacman -Ss ^"${_c[@]/#/python-}"$ 2>/dev/null)" ]];then
        checkdepends+=( "${_c[@]/#/python-}")
    fi
    if [[ -n "$(pacman -Ss ^"${_c[@]/#/python2-}"$ 2>/dev/null)" ]];then
        checkdepends+=( "${_c[@]/#/python-}")
    fi
done

export SETUPTOOLS_INSTALL_WINDOWS_SPECIFIC_FILES=0

prepare() {
    rm -r setuptools-$pkgver/{pkg_resources,setuptools}/{extern,_vendor}

    # Upstream devendoring logic is badly broken, see:
    # https://bugs.archlinux.org/task/58670
    # https://github.com/pypa/pip/issues/5429
    # https://github.com/pypa/setuptools/issues/1383
    # The simplest fix is to simply rewrite import paths to use the canonical
    # location in the first place
    for _module in setuptools pkg_resources '' ; do
        find setuptools-$pkgver/$_module -name \*.py -exec sed -i \
             -e 's/from '$_module.extern' import/import/' \
             -e 's/from '$_module.extern'./from /' \
             -e 's/import '$_module.extern'./import /' \
             -e "s/__import__('$_module.extern./__import__('/" \
             {} +
    done

    # Remove post-release tag since we are using stable tags
    sed -e '/tag_build = .post/d' \
        -e '/tag_date = 1/d' \
        -i setuptools-$pkgver/setup.cfg

    # 'Clean' installation is expected to fail since we removed bundled packages
    sed -i '/^def test_clean_env_install/i import pytest\n\n@pytest.mark.xfail' setuptools-$pkgver/setuptools/tests/test_virtualenv.py

    # Tests failed. Importing an unbundled new setuptools in a virtualenv does not work, but this won't
    # affect normal virtualenv usage (which don't have to import the unbundled setuptools in *current*
    # dir.
    sed -e '/^def test_pip_upgrade_from_source/i @pytest.mark.xfail' \
        -e '/^def test_test_command_install_requirements/i @pytest.mark.xfail' \
        -e '/^def test_no_missing_dependencies/i @pytest.mark.xfail' \
        -i setuptools-$pkgver/setuptools/tests/test_virtualenv.py

    cp -a setuptools-$pkgver{,-py2}

    cd "$srcdir"/setuptools-$pkgver
    sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py

    cd "$srcdir"/setuptools-$pkgver-py2
    sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
}

build() {
    cd "$srcdir"/setuptools-$pkgver
    python bootstrap.py
    python setup.py build

    cd "$srcdir"/setuptools-$pkgver-py2
    python2 bootstrap.py
    python2 setup.py build
}

check() { (
            # Workaround UTF-8 tests by setting LC_CTYPE
            export LC_CTYPE=en_US.UTF-8

            # https://github.com/pypa/setuptools/pull/810
            export PYTHONDONTWRITEBYTECODE=1

            cd "$srcdir"/setuptools-$pkgver
            python setup.py pytest

            cd "$srcdir"/setuptools-$pkgver-py2
            python2 setup.py pytest
        )}

package_python-setuptools() {
    provides=('python-distribute')
    replaces=('python-distribute')

    cd "$srcdir"/setuptools-$pkgver
    python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-setuptools() {
    provides=('python2-distribute')
    replaces=('python2-distribute')

    cd "$srcdir"/setuptools-$pkgver-py2
    python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
    rm "$pkgdir"/usr/bin/easy_install
}
