#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgbase=python-anytree
pkgname=(python-anytree python2-anytree)
pkgver=2.6.0
pkgrel=1
pkgdesc="Powerful and Lightweight Python Tree Data Structure"
url="https://anytree.readthedocs.io/"
arch=(any)
license=(Apache)
makedepends=(python{,2}-{six,setuptools} git)
_commit=775477e206a75e697983e70dae6372b5a7e42dcf  # tags/2.6.0
source=("$pkgbase::git+https://github.com/c0fec0de/anytree#commit=$_commit")
sha256sums=('SKIP')
groups=('extra')

pkgver() {
    cd $pkgbase
    git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
    cp -a $pkgbase python2
    cp -a $pkgbase python3
}

build() {
    cd python3
    python3 setup.py build

    cd ../python2
    python2 setup.py build
}

package_python-anytree() {
    depends=(python-six)
    groups+=( 'python-extra')

    cd python3
    python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-anytree() {
    depends=(python2-six)
    groups+=( 'python-extra')

    cd python2
    python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
