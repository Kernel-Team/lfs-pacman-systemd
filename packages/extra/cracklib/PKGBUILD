# -*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=cracklib
pkgver=2.9.7
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="https://github.com/cracklib/cracklib"
depends=('glibc' 'zlib')
source=(https://github.com/cracklib/cracklib/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/cracklib/cracklib/releases/download/v$pkgver/$pkgname-words-$pkgver.gz
       )
noextract=($pkgname-words-$pkgver.gz)
install=cracklib.install

prepare(){
    cd $srcdir/$pkgname-$pkgver

    # Remove a meaningless warning
    sed -i '/skipping/d' util/packer.c
    
    ./configure --prefix=/usr --disable-static --with-default-dict=/lib/cracklib/pw_dict
}

build() {
    cd $srcdir/$pkgname-$pkgver
    make 
}

check() {
    cd $srcdir/$pkgname-$pkgver
    make check
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install

    install -v -dm755 $pkgdir/lib
    mv -v $pkgdir/usr/lib/libcrack.so.* $pkgdir/lib
    ln -sfv ../../lib/$(readlink $pkgdir/usr/lib/libcrack.so) $pkgdir/usr/lib/libcrack.so
    ln -sv ../../lib/$(readlink ${pkgdir}/lib/libcrack.so.2) ${pkgdir}/usr/lib/libcrack.so.2

    install -v -m644 -D ${srcdir}/$pkgname-words-$pkgver.gz $pkgdir/usr/share/dict/$pkgname-words.gz

    gunzip -v $pkgdir/usr/share/dict/$pkgname-words.gz
    ln -v -sf $pkgname-words $pkgdir/usr/share/dict/words
    echo $(hostname) >> $pkgdir/usr/share/dict/$pkgname-extra-words
    install -v -m755 -d $pkgdir/lib/cracklib

    # symlink cracklib-small #34778
    ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
    sh ./util/cracklib-format dicts/cracklib-small \
        | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}

md5sums=('48a0c8810ec4780b99c0a4f9931c21c6'
         '34ca7bdb64f0ccb19b9d68243ada7919')
