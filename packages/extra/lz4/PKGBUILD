#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=lz4
pkgver=1.9.2
pkgrel=1
pkgdesc='Extremely fast compression algorithm'
arch=('x86_64')
url='http://www.lz4.org/'
license=('GPL2')
makedepends=()
checkdepends=('diffutils')
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lz4/lz4/archive/v$pkgver.tar.gz")
groups=('extra')

build() {
    # do not use the main makefile, it calls sub make with -e
    # exported CLFAGS by makepkg break the version. see FS#50071
    cd $pkgname-$pkgver
    make -C lib PREFIX=/usr
    make -C programs PREFIX=/usr lz4 lz4c
}

check() {
    rm -f passwd.lz4
    $pkgname-$pkgver/programs/lz4 /etc/passwd passwd.lz4
    $pkgname-$pkgver/programs/lz4 -d passwd.lz4 passwd
    diff -q /etc/passwd passwd
    rm passwd
}

package() {
    cd $pkgname-$pkgver
    make install PREFIX=/usr DESTDIR="$pkgdir"
}

md5sums=('3898c56c82fb3d9455aefd48db48eaad')
