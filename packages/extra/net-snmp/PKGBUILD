#-*-mode:sh-*-
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
pkgname=net-snmp
pkgver=5.8
pkgrel=1
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('x86_64')
url="http://www.net-snmp.org/"
license=('BSD')
depends=('libnsl' 'libpcap' 'lm_sensors' 'pciutils' 'pcre')
makedepends=('python2-setuptools')
optdepends=('perl-term-readkey: for snmpcheck application'
            'perl-tk: for snmpcheck and tkmib applications'
            'python2: for the python modules')
options=('!emptydirs' '!makeflags')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        snmpd.service snmptrapd.service)
validpgpkeys=('8AAA779B597B405BBC329B6376CF47B8A77C5329'
              '27CAA4A32E371383A33ED0587D5F9576E0F81533'
              'D0F8F495DA6160C44EFFBF10F07B9D2DACB19FD6')  # Net-SNMP Administrators
groups=('extra')

prepare() {
    cd ${pkgname}-${pkgver}
    autoreconf -i
    PYTHONPROG=/usr/bin/python2 ./configure --prefix=/usr/local \
              --sysconfdir=/usr/local/etc \
              --mandir=/usr/local/share/man \
              --enable-ucd-snmp-compatibility \
              --enable-ipv6 \
              --with-python-modules \
              --with-default-snmp-version="3" \
              --with-sys-contact="root@localhost" \
              --with-sys-location="Unknown" \
              --with-logfile="/var/log/snmpd.log" \
              --with-mib-modules="host misc/ipfwacc ucd-snmp/diskio tunnel ucd-snmp/dlmod ucd-snmp/lmsensorsMib" \
              --with-persistent-directory="/var/net-snmp"
}

build() {
    cd ${pkgname}-${pkgver}
    make NETSNMP_DONT_CHECK_VERSION=1
}

package() {
    cd ${pkgname}-${pkgver}
    sed -i -e "s:install --basedir=\$\$dir:install --basedir=\$\$dir --root=${pkgdir}:" Makefile
    make DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}" INSTALLDIRS=vendor install
    install -D -m644 "${srcdir}/snmpd.service" "${pkgdir}/lib/systemd/system/snmpd.service"
    install -D -m644 "${srcdir}/snmptrapd.service" "${pkgdir}/lib/systemd/system/snmptrapd.service"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -vdm755 ${pkgdir}/usr/lib
    mv -v ${pkgdir}/usr/local/lib/python2.7 ${pkgdir}/usr/lib
}

md5sums=('63bfc65fbb86cdb616598df1aff6458a'
         'SKIP'
         '6983918a91bdc3b73b0b551f201b27d9'
         'bdb99a62b8e33d60e42ecba0ccbac208')
