#-*-mode:sh-*-
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=13e0084
pkgver=20190709
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
license=('GPL')
url="http://www.skbuff.net/iputils/"
groups=('extra' 'lfs-base')
depends=('openssl' 'libcap' 'libidn2')
optdepends=('xinetd: for tftpd')
makedepends=('perl-sgmls' 'git' 'docbook-xsl' 'meson')
conflicts=('netkit-base' 'arping' 'netkit-tftpd')
replaces=('netkit-base')
backup=(etc/local/xinetd.d/tftp)
install=${pkgname}.install
source=("git+https://github.com/iputils/iputils.git#commit=${_rev}"
        tftp.xinetd)

build() {
    mkdir -p build
    cd build

    meson setup \
          --prefix /usr/local \
          --libdir /usr/local/lib \
          --sysconfdir /usr/local/etc \
          --localstatedir /var \
          ../$pkgname -DBUILD_RARPD=true
    ninja
}

package() {
    cd build

    DESTDIR="$pkgdir" ninja install

    # FS#24768
    install -dm755 "${pkgdir}"/etc/local/xinetd.d/
    install -m644 "${srcdir}"/tftp.xinetd "${pkgdir}"/etc/local/xinetd.d/tftp
}

sha1sums=('SKIP'
          '40da8a6f7c71d7466f65a435caa2ae3e7fdb3c14')
