#-*-mode:sh-*-
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: alexforsale@yahoo.com
pkgname=haskell-primitive
pkgver=0.6.4.0
pkgrel=1
pkgdesc="Wrappers for primitive operations"
url="https://hackage.haskell.org/package/primitive"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/primitive/${pkgver}/primitive-${pkgver}.tar.gz")
sha512sums=('a9d31b386c41abd4a5e601883b6f4d230c9669d1eda840a3491fc2e5961e8bce0c2bc1026426b92a36790cd372db954b3003499c540605289540e26543e12b80')
groups=('extra' 'haskell-extra')

prepare() {
    cd primitive-$pkgver
    sed -i 's/< *4.12/<5/' primitive.cabal
}

build() {
    cd primitive-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
               --prefix=/usr/local --docdir=/usr/local/share/doc/$pkgname --enable-tests \
               --dynlibdir=/usr/local/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd primitive-$pkgver
    install -D -m744 register.sh "$pkgdir"/usr/local/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/local/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
    rm -f "$pkgdir"/usr/local/share/doc/${pkgname}/LICENSE
}
