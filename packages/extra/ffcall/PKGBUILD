#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=ffcall
pkgver=2.1
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('x86_64')
url="http://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!makeflags' 'staticlibs')
source=(https://ftp.gnu.org/gnu/libffcall/libffcall-${pkgver}.tar.gz)
groups=('extra')

prepare(){
    cd "${srcdir}/lib${pkgname}-${pkgver}"
    [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
    ./configure --prefix=/usr/local --mandir=/usr/local/share/man $CONFIGFLAG
}

build() {
    cd "${srcdir}/lib${pkgname}-${pkgver}"
    make
}

check() {
    cd "${srcdir}/lib${pkgname}-${pkgver}"
    make check
}

package() {
    cd "${srcdir}/lib${pkgname}-${pkgver}"
    install -d "${pkgdir}"/usr/local/share/{man,doc/ffcall}
    make DESTDIR="${pkgdir}" htmldir=/usr/local/share/doc/ffcall install
}

md5sums=('0bc45ab055d40ba617f47e117084499f')
