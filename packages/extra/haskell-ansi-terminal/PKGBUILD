#-*-mode:sh-*-
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Contributor: alexforsale@yahoo.com
_hkgname=ansi-terminal
pkgname=haskell-ansi-terminal
pkgver=0.9.1
pkgrel=1
pkgdesc="Simple ANSI terminal support, with Windows compatibility"
url="https://github.com/feuerbach/ansi-terminal"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
_deps=('haskell-colour')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bab9b55fe0494b30c01ab9211bfb17a02eaea781a945175b3b511f876004e27124b9caa71c185a8643bbff31dee814bd410ce66742dd930328533d3a63e785a0')
groups=('extra' 'haskell-extra')

if [[ $(pacman -Ss ^"$_deps"$ 2>/dev/null) ]];then
    depends+=( $_deps)
    pkgrel=$((pkgrel+1))
else
    optdepends+=( $_deps)
fi

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
               --prefix=/usr/local --docdir=/usr/local/share/doc/$pkgname \
               --dynlibdir=/usr/local/lib --libsubdir=\$compiler/site-local/\$pkgid \
               -f-example
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/local/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/local/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/local/share/doc/${pkgname}/LICENSE"
}
