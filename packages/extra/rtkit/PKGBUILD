#-*-mode:sh-*-
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: alexforsale@yahoo.com
pkgname=rtkit
pkgver=0.12
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=(x86_64)
url="https://github.com/heftig/rtkit"
license=(GPL3 'custom:BSD')
depends=(dbus polkit systemd)
makedepends=(git)
_commit=4c7e5ca63b1278b41c31b63a4ca5cce331d9db2e  # tags/v0.12^0
source=("git+https://github.com/heftig/rtkit#commit=$_commit")
sha256sums=('SKIP')
groups=('extra')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
    cd $pkgname
    ./autogen.sh
    ./configure \
        --prefix=/usr/local \
        --sysconfdir=/usr/local/etc \
        --libexecdir=/usr/local/lib \
        --with-systemdsystemunitdir=/lib/systemd/system
}

build() {
    cd $pkgname
    make
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir/usr/local/lib/installed-tests"

    echo 'u rtkit 133 "RealtimeKit" /proc' |
        install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
    sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/$pkgname/COPYING"

    install -vdm755 ${pkgdir}/usr/share
    mv -v ${pkgdir}/usr/local/share/dbus-1 ${pkgdir}/usr/share
    mv -v ${pkgdir}/usr/local/share/polkit-1 ${pkgdir}/usr/share
}
