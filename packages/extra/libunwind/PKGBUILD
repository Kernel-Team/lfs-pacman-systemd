#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=libunwind
pkgver=1.3.1
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('x86_64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('glibc' 'xz')
_makedeps=('texlive-core')
makedepends=()
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
validpgpkeys=('5C96BDEAF5F47FB02BD4F6B965D98560914F3F48'  # Arun Sharma
              '1675C8DA2EF907FB116EB709EC52B396E6874AF2') # Dave Watson
groups=('extra')

for _m in "${_makedeps[@]}";do
    if [[ "$(pacman -Qq "${_m}" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
    else
        optdepends+=( "${_m}")
    fi
done

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr
    make
}

check() {
    cd $pkgname-$pkgver
    # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
    make check || :
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}

sha512sums=('3110d0aed4f5c781ef1ff72c9337e59793c02c42066209a4ac44f50eff1c0b0e02a5ff9f66891e62016de14af065a47975763970b839b700c0ff2e9f415c8def'
            'SKIP')
