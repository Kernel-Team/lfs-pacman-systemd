#-*-mode:sh-*-
# Maintainer: alexforsale@yahoo.com
pkgname=python-sphinx
pkgver=2.2.0
pkgrel=1
arch=('any')
url='http://www.sphinx-doc.org/'
license=('BSD')
depends=('python-babel'
         'python-docutils'
         'python-imagesize'
         'python-jinja'
         'python-pygments'
         'python-requests'
         'python-setuptools'
         'python-snowballstemmer'
         'python-sphinx-alabaster-theme'
         'python-sphinx_rtd_theme'
         'python-sphinxcontrib-'{{apple,dev,html}help,jsmath,qthelp,serializinghtml})
checkdepends=('imagemagick' 'librsvg'
              'python-html5lib'
              'python-pytest'
              'texlive-fontsextra' 'texlive-latexextra')
optdepends=('imagemagick: for ext.imgconverter'
            'texlive-latexextra: for generation of PDF documentation')
source=("https://pypi.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz"{,.asc}
        sphinx-graphviz.patch::"https://patch-diff.githubusercontent.com/raw/sphinx-doc/sphinx/pull/5549.patch")
validpgpkeys=('8A11B79A5D0D749A66F0A030102C2C17498D6B9E'
              'E9BEABB07E7B9CC3F56E62C91425F8CE5EBA0E07')
groups=('extra' 'python-extra')

build() {
  cd Sphinx-$pkgver
  make build
}

check() {
  cd Sphinx-$pkgver
  LC_ALL="en_US.UTF-8" make test
  rm -r tests
}

package() {
  cd Sphinx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}


md5sums=('ca64efdbb4d3a0a78398dbcf9f78971d'
         'SKIP'
         'abe4f5fa6dc1347040f53729bb21ed65')
