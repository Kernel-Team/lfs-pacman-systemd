#-*-mode:sh-*-
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: alexforsale@yahoo.com
pkgname=graphene
pkgver=1.10.0
pkgrel=1
pkgdesc="A thin layer of graphic data types"
url="https://ebassi.github.io/graphene/"
arch=(x86_64)
license=(MIT)
depends=(glib2)
makedepends=(git gtk-doc gobject-introspection meson)
_commit=6099dbb39ff8de20f37c8ca6e951203a810c1649  # tags/1.10.0^0
source=("git+https://github.com/ebassi/graphene#commit=$_commit"
        "git+https://github.com/ebassi/mutest")
sha256sums=('SKIP'
            'SKIP')
groups=('extra')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $pkgname

    git submodule init
    git config --local submodule.subprojects/mutest.url "$srcdir/mutest"
    git submodule update

    cd ..
    meson setup \
          --prefix /usr/local \
          --libdir /usr/local/lib \
          --sysconfdir /usr/local/etc \
          $pkgname build \
                 -D installed_tests=false #-D gobject_types=false #-D gtk_doc=true
}

build() {
    ninja -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" meson install -C build
    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/LICENSE.txt
}

# vim:set sw=2 et:
