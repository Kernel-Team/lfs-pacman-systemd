#-*-mode:sh-*-
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Contributor: alexforsale@yahoo.com
_hkgname=attoparsec
pkgname=haskell-attoparsec
pkgver=0.13.2.3
pkgrel=1
pkgdesc="Fast combinator parsing for bytestrings and text"
url="https://github.com/bos/attoparsec"
license=("BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-scientific')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-unicode' 'haskell-tasty'
             'haskell-tasty-quickcheck' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('84805ff547f1aa5eebd320c1bd738bc1098a9789fb330eff7bdcb1b0477e9709d86f93bd8848f4a8501a123b6add6bd22af59cfa94af66f1d57703745706a194')
groups=('extra' 'haskell-extra')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
               --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
               --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
               -f-developer
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed, possibly due to unmet dependency version on QuickCheck"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
